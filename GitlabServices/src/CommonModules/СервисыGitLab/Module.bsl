////////////////////////////////////////////////////////////////////////////////
//
// Работа с сервисами GitLab.
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

Процедура ЗапуститьОбработкуДанныхВФоне(Знач ИдентификаторWebhook,
											Знач ДанныеТелаЗапроса,
											Знач ЭтоРучнойЗапуск = Ложь) Экспорт
	
	Перем ТекстСообщения;
	Перем ПараметрыПроектаРепозитория;
	Перем КлючЗапросаGitLab;	
	Перем ПараметрыЗадания;
	Перем АктивныеЗадания;
	Перем ДополнительныеПараметры;
	
	ДополнительныеПараметры = Логирование.ДополнительныеДанные();
	ДополнительныеПараметры.Объект = ИдентификаторWebhook;
	
	Если ТипЗнч(ДанныеТелаЗапроса) <> Тип("Соответствие") Тогда
		
		ТекстСообщения = НСтр("ru = 'Неверный формат данных в теле запроса.'");
		Логирование.Ошибка( "Core.ОбработкаДанныхВФоне", ТекстСообщения, ДополнительныеПараметры );
		
		Возврат;
		
	КонецЕсли;
	
	КлючЗапросаGitLab = ДанныеТелаЗапроса.Получить("checkout_sha");
	
	Если КлючЗапросаGitLab = Неопределено Тогда
		
		ТекстСообщения = НСтр("ru = 'Запрос не идентифицирован (отсутствует checkout_sha).'");
		Логирование.Ошибка( "Core.ОбработкаДанныхВФоне", ТекстСообщения, ДополнительныеПараметры );
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыПроектаРепозитория = ПараметрыПроектаИзТелаЗапроса(ДанныеТелаЗапроса);
	АктивныеЗадания             = АктивныеЗадачиПоКлючу(КлючЗапросаGitLab);
	
	Если ЗначениеЗаполнено(АктивныеЗадания) Тогда
		
		ТекстСообщения = НСтр("ru = '%1: задание уже было запущено и активно.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, КлючЗапросаGitLab);
		Логирование.Предупреждение( "Core.ОбработкаДанныхВФоне", ТекстСообщения, ДополнительныеПараметры );
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить(ИдентификаторWebhook);
	ПараметрыЗадания.Добавить(КлючЗапросаGitLab);
	ПараметрыЗадания.Добавить(ДанныеТелаЗапроса);
	ПараметрыЗадания.Добавить(ПараметрыПроектаРепозитория);
	ПараметрыЗадания.Добавить(ЭтоРучнойЗапуск);

	// Перехват всех ошибок с логированием происходит внутри фонового.
	// Работу текущей процедуры не заворачивать в попытка...исключение. 
	ФоновыеЗадания.Выполнить("СервисыGitLab.ФоноваяОбработкаДанныхЗапроса",
							 ПараметрыЗадания,
							 КлючЗапросаGitLab,
							 "СервисыGitLab: " + КлючЗапросаGitLab);
							 
	ТекстСообщения = НСтр("ru = '%1: запущена обработка данных. Ручной запуск: %2.'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, КлючЗапросаGitLab, Строка(ЭтоРучнойЗапуск));

	Логирование.Информация("Core.ОбработкаДанныхВФоне.Начало", ТекстСообщения, ДополнительныеПараметры);
	
КонецПроцедуры


#Область Webhooks




// Сохраняет исходные данные HTTP-запроса (См. РегистрСведений.ОбработчикиСобытий).
// 
// Параметры:
// 	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
// 	Ключ - Строка - значение checkout_sha коммита;
// 	ДанныеТелаЗапроса - (См. ПолучитьДанныеТелаЗапроса.ДанныеТелаЗапроса);
Процедура СохранитьДанныеТелаЗапросаВИБ(Знач ИдентификаторWebhook, Знач Ключ, Знач ДанныеТелаЗапроса) Экспорт
	
	СохранитьДанныеВИБ(ИдентификаторWebhook, Ключ, "ДанныеТелаЗапроса", ДанныеТелаЗапроса);

КонецПроцедуры

// Сохраняет двоичные данные внешних файлов с их описанием (См. РегистрСведений.ОбработчикиСобытий).
// 
// Параметры:
// 	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
// 	Ключ - Строка - значение checkout_sha коммита;
// 	ДвоичныеДанные - (См. ПодготовитьДанныеДляОтправки);
Процедура СохранитьДвоичныеДанныеВИБ(Знач ИдентификаторWebhook, Знач Ключ, Знач ДвоичныеДанные) Экспорт
	
	СохранитьДанныеВИБ(ИдентификаторWebhook, Ключ, "ДвоичныеДанные", ДвоичныеДанные);

КонецПроцедуры

#КонецОбласти

#Область НастройкиСервисов

// Подготавливает структуру со всеми текущими настройками механизмов управления сервисами GitLab.
// 
// Параметры:
// Возвращаемое значение:
// ФиксированнаяСтруктура - описание:
// * ТаймаутИБПолучателя - Число - (См. Константа.ТаймаутИБПолучателя);
// * ТаймаутGitLab - Число - (См. Константа.ТаймаутGitLab);
// * AccessTokenReceiver - Строка - (См. Константа.AccessTokenReceiver);
// * ИмяФайлаНастроекМаршрутизации - Строка - (См. Константа.ИмяФайлаНастроекМаршрутизацииGitLab);
// * GitLabUserPrivateToken - Строка - (См. Константа.GitLabUserPrivateToken);
// * ЗагружатьФайлыИзВнешнегоХранилища - Булево - (См. Константа.ЗагружатьФайлыИзВнешнегоХранилища);
Функция НастройкиСервисовGitLab() Экспорт
	
	Перем Настройки;
	Перем ТекстСообщения;
	
	Настройки = Новый Структура;
	Настройки.Вставить("ЗагружатьФайлыИзВнешнегоХранилища", Ложь);
	Настройки.Вставить("GitLabUserPrivateToken", "");
	Настройки.Вставить("ИмяФайлаНастроекМаршрутизации", "");
	Настройки.Вставить("AccessTokenReceiver", "");
	Настройки.Вставить("ТаймаутGitLab", 0);
	Настройки.Вставить("ТаймаутИБПолучателя", 0);
//	Настройки.Вставить("МестоположениеСервисаИБПолучателя", "");
	
	Попытка
		
		Настройки.ЗагружатьФайлыИзВнешнегоХранилища = Константы.ЗагружатьФайлыИзВнешнегоХранилища.Получить();
		Настройки.GitLabUserPrivateToken            = GitLabUserPrivateToken();
		Настройки.ИмяФайлаНастроекМаршрутизации     = ИмяФайлаНастроекМаршрутизации();
		Настройки.AccessTokenReceiver    = AccessTokenReceiver();
		Настройки.ТаймаутGitLab       = ТаймаутGitLab();
		Настройки.ТаймаутИБПолучателя = ТаймаутИБПолучателя();
		//Настройки.МестоположениеСервисаИБПолучателя = МестоположениеСервисаИБПолучателя();
		
		Настройки = Новый ФиксированнаяСтруктура(Настройки);
		
	Исключение
		ТекстСообщения = СтрШаблон(НСтр("ru = '%1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Логирование.Ошибка( "System.Настройки", ТекстСообщения );
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#Область ФоновыеЗадания

// Поиск всех фоновых заданий по наименованию.
// 
// Параметры:
// 	Наименование - Строка - наименование фонового задания;
// Возвращаемое значение:
// 	- Массив из ФоновоеЗадание - найденные фоновые задания;
Функция ЗадачиПоНаименованию(Знач Наименование) Экспорт
	
	Перем ПараметрыОтбора;
	Перем ВсеФоновыеЗадания;
	
	ПараметрыОтбора = Новый Структура("Наименование", Наименование);
	ВсеФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(ПараметрыОтбора);
	
	Возврат ВсеФоновыеЗадания;
	
КонецФункции

#КонецОбласти

#Область ДанныеЗапроса

// Подготавливает необходимые данные для формирования запроса к api GitLab на получение данных по всем запросам на merge.  
// 
// Параметры:
//	ДанныеТелаЗапроса - Соответствие - преобразованные в Соответствие данные HTTP-запроса;
// Возвращаемое значение:
// 	Структура - Описание:
// * СекретныйКлюч - Строка - ключ пользователя с доступом к api GitLab;
// * URL - Строка - веб-адрес запроса на merge;
Функция СформироватьЗапросНаMergeRequests(Знач ДанныеТелаЗапроса) Экспорт
	
	Перем ПараметрыПроекта;
	
	Перем Схема;
	Перем ИмяСервера;
	Перем Проект;
	
	Перем Запрос;
	
	ПараметрыПроекта = ПараметрыПроектаИзТелаЗапроса(ДанныеТелаЗапроса);
	
	Схема      = ПараметрыПроекта.СтруктураURI.Схема;
	ИмяСервера = ПараметрыПроекта.СтруктураURI.Сервер;
	Проект     = ПараметрыПроекта.Идентификатор;
	
	Запрос = Новый Структура;
	Запрос.Вставить("URL", СтрШаблон("%1://%2/api/v4/projects/%3/merge_requests", Схема, ИмяСервера, Проект));
	Запрос.Вставить("СекретныйКлюч", GitLabUserPrivateToken());
	
	Возврат Запрос;
	
КонецФункции

// Получение данных тела запроса по идентификационному номеру коммита.
// 
// Параметры:
//	ДанныеТелаЗапроса - Соответствие - преобразованные в Соответствие данные HTTP-запроса;
// 	ИскомыйИдентификатор - Строка - идентификационный номер коммита;
// Возвращаемое значение:
// 	Неопределено, Соответствие - возвращает коллекцию данных по коммиту, либо Неопределено, если данные не найдены; 
Функция ПолучитьДанныеКоммитаИзТелаЗапроса(Знач ДанныеТелаЗапроса, Знач ИскомыйИдентификатор = "") Экспорт
	
	Перем Результат;
	Перем Коммиты;
	Перем Идентификатор;
	
	Результат = Неопределено;
	
	Если ТипЗнч(ДанныеТелаЗапроса) <> Тип("Соответствие") ИЛИ ПустаяСтрока(ИскомыйИдентификатор) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Коммиты = ДанныеТелаЗапроса.Получить("commits");
	
	Если Коммиты = Неопределено ИЛИ ТипЗнч(Коммиты) <> Тип("Массив") Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для каждого Коммит Из Коммиты Цикл

		Идентификатор = Коммит.Получить("id");
		Если Идентификатор = Неопределено ИЛИ Идентификатор <> ИскомыйИдентификатор Тогда
			Продолжить;
		КонецЕсли;
		
		Результат = Коммит;
		
		Прервать;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Проверяет наличие записи о пользовательских настройках маршрутизации в данных тела запроса для определенного коммита.
// 
// Параметры:
//	ДанныеТелаЗапроса - Соответствие - преобразованные в Соответствие данные HTTP-запроса;
// 	ИдентификаторКоммита - Строка - идентификационный номер коммита;
// Возвращаемое значение:
// 	Булево - Истина, пользовательские настройки для коммита существуют, иначе - Ложь;
Функция ЕстьПользовательскиеНастройкиВДанныхКоммита(Знач ДанныеТелаЗапроса, Знач ИдентификаторКоммита) Экспорт
	
	Перем ДанныеКоммита;
	Перем Результат;
	
	Результат = Ложь;
	
	ДанныеКоммита = ПолучитьДанныеКоммитаИзТелаЗапроса(ДанныеТелаЗапроса, ИдентификаторКоммита);
	Если ДанныеКоммита = Неопределено ИЛИ ТипЗнч(ДанныеКоммита) <> Тип("Соответствие") Тогда
		Возврат Результат;
	КонецЕсли;
		
	ПользовательскиеНастройки = ДанныеКоммита.Получить("user_settings");
	
	Если ПользовательскиеНастройки <> Неопределено Тогда
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Удаляет пользовательский вариант настроек маршрутизации для определенного коммита в данных тела запроса.
// 
// Параметры:
//	ДанныеТелаЗапроса - Соответствие - преобразованные в Соответствие данные HTTP-запроса;
// 	ИдентификаторКоммита - Строка - идентификационный номер коммита;
// 	НастройкиПоУмолчанию - Строка - (возвращаемый параметр), текст настройки маршрутизации в формате JSON
// 		из файла настроек внешнего хранилища;
Процедура УдалитьПользовательскиеНастройкиВДанныхКоммита(Знач ДанныеТелаЗапроса,
															Знач ИдентификаторКоммита,
															НастройкиПоУмолчанию = "") Экспорт
	
	Перем ДанныеКоммита;
	Перем Настройки;
	
	ДанныеКоммита = ПолучитьДанныеКоммитаИзТелаЗапроса(ДанныеТелаЗапроса, ИдентификаторКоммита);
	Если ДанныеКоммита = Неопределено ИЛИ ТипЗнч(ДанныеКоммита) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеКоммита.Удалить("user_settings");
	Настройки = ДанныеКоммита.Получить("settings");
	
	Если Настройки <> Неопределено Тогда
		НастройкиПоУмолчанию = Настройки.Получить("json");
	КонецЕсли;

КонецПроцедуры

// Добавляет пользовательский вариант настроек маршрутизации для определенного коммита в данных тела запроса.
// 
// Параметры:
//	ДанныеТелаЗапроса - Соответствие - (возвращаемое значение), преобразованные в Соответствие данные HTTP-запроса;
// 	ИдентификаторКоммита - Строка - идентификационный номер коммита;
// 	JSON - Строка - текст настроек маршрутизации в формате JSON;
Процедура ДополнитьДанныеТелаЗапросаПользовательскимиНастройками(Знач ИдентификаторКоммита,
																	Знач JSON,
																	ДанныеТелаЗапроса) Экспорт
	
	Перем ДанныеКоммита;
	Перем ПользовательскиеНастройки;
	Перем Запись;
	Перем Поток;
	
	ДанныеКоммита = ПолучитьДанныеКоммитаИзТелаЗапроса(ДанныеТелаЗапроса, ИдентификаторКоммита);
	
	Если ДанныеКоммита = Неопределено ИЛИ ТипЗнч(ДанныеКоммита) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;

	Поток = Новый ПотокВПамяти();
	Запись = Новый ЗаписьДанных(Поток);
	Запись.ЗаписатьСтроку(JSON);
	Поток.Перейти(0, ПозицияВПотоке.Начало);

	ПользовательскиеНастройки = Неопределено;
	ОбщегоНазначения.ПотокВКоллекциюКакJSON(Поток, Истина, Истина, ПользовательскиеНастройки);
	
	Если ПользовательскиеНастройки <> Неопределено Тогда
		ДанныеКоммита.Вставить("user_settings", ПользовательскиеНастройки);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

// Получение исходных данных запроса и настроек маршрутизации в формате json из сохраненных данных запроса.
// 
// Параметры:
// 	КлючЗаписи - РегистрСведенийКлючЗаписи.ОбработчикиСобытий - ключ записи с сохраненными записями;
// 	Действие - "ОткрытьJSONТелаЗапроса", "ОткрытьJSONМаршрутизации";
// Возвращаемое значение:
// 	- Неопределено - если не найдена запись с данными на регистре сведений; 
// 	- Строка - json HTTP-запроса;
// 	- ТаблицаЗначений - таблица настроек маршрутизации в формате json:
// * Коммит - Строка - номер коммита; 
// * ТекстJSON - Строка - json настроек маршрутизации;
Функция ИсходныеДанныеЗапросаВФорматеJSON(Знач КлючЗаписи, Знач Действие) Экспорт
	
	Перем ДанныеТелаЗапроса;
	Перем Настройки;
	Перем НоваяСтрока;
	Перем Результат;
	
	Результат = Неопределено;

	ДанныеТелаЗапроса = РегистрыСведений.ДанныеОбработчиковСобытий.ПолучитьДанныеТелаЗапроса(КлючЗаписи);
	Если ДанныеТелаЗапроса = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Действие = "ОткрытьJSONТелаЗапроса" Тогда
		
		Результат = "";
		Результат = ДанныеТелаЗапроса.Получить("json");
		
	ИначеЕсли Действие = "ОткрытьJSONМаршрутизации" Тогда
		
		Результат = Новый ТаблицаЗначений;
		Результат.Колонки.Добавить("Коммит", Новый ОписаниеТипов("Строка"));
		Результат.Колонки.Добавить("ТекстJSON", Новый ОписаниеТипов("Строка"));
		Результат.Колонки.Добавить("ПользовательскийВариант", Новый ОписаниеТипов("Булево"));
		
		Коммиты = ДанныеТелаЗапроса.Получить("commits");
		Для каждого Коммит Из Коммиты Цикл
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.Коммит = Коммит.Получить("id");
			
			ПользовательскиеНастройки = Коммит.Получить("user_settings");
			ЕстьПользовательскиеНастройки = ?(ПользовательскиеНастройки <> Неопределено, Истина, Ложь);
			
			Если ЕстьПользовательскиеНастройки Тогда
				Настройки = ПользовательскиеНастройки;
			Иначе
				Настройки = Коммит.Получить("settings");
			КонецЕсли;
			
			Если Настройки = Неопределено Тогда
				НоваяСтрока.ТекстJSON = "";
				Продолжить;
			КонецЕсли;
			НоваяСтрока.ТекстJSON = Настройки.Получить("json");
			НоваяСтрока.ПользовательскийВариант = ЕстьПользовательскиеНастройки;
		КонецЦикла;
		
	Иначе
		
		Результат = Неопределено;
		
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

// Инициализирует параметры доставки двоичного файла в ИБ-приемник через веб-сервис.
// 
// Параметры:
// Возвращаемое значение:
// 	Структура - Описание:
// * ТочкаДоставки - Строка - end-point сервиса загрузки внешних отчетов/обработок;
// * Таймаут - Число - таймаут запроса, в секундах, если 0, то не установлен;
// * Token - Строка - token доступа к получателю;
//
Функция ИнициализироватьПараметрыДоставки() Экспорт
	
	Перем ПараметрыДоставки;
	
	ПараметрыДоставки = Новый Структура;
	ПараметрыДоставки.Вставить("ТочкаДоставки", "");
	ПараметрыДоставки.Вставить("Token", "");
	ПараметрыДоставки.Вставить("Таймаут", 0);
	
	Возврат ПараметрыДоставки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НастройкиМаршрутизации

// Дополняет каждый коммит данных разобранного тела запроса настройками маршрутизации,
// полученными из файла, расположенном в корне репозитория. Для каждого коммита берется своя версия файла.
// 
// Параметры:
//	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
//	ПараметрыПроекта - (См. ПараметрыПроектаИзТелаЗапроса);
//	Таймаут - (См. ТаймаутGitLab);
// 	ДанныеТелаЗапроса - (См. ПолучитьДанныеТелаЗапроса.ДанныеТелаЗапроса);
Процедура ДополнитьДанныеЗапросаНастройками(Знач ИдентификаторWebhook,
												Знач ПараметрыПроекта,
												Знач Таймаут = 0,
												ДанныеТелаЗапроса)
	
	Перем ОписаниеФайловНастроек;
	Перем Коммиты;
	Перем Идентификатор;
	Перем Настройка;
	
	PrivateToken           = GitLabUserPrivateToken();
	ОписаниеФайловНастроек = ОписаниеФайловНастроекМаршрутизации(ДанныеТелаЗапроса, ПараметрыПроекта);
	НастройкиМаршрутизации = НастройкиМаршрутизации(ИдентификаторWebhook,
								ОписаниеФайловНастроек,
								ПараметрыПроекта,
								PrivateToken,
								Таймаут);
	
	Коммиты = ДанныеТелаЗапроса.Получить("commits");
	
	Для каждого Коммит Из Коммиты Цикл
		
		Идентификатор = Коммит.Получить("id");
		Настройка     = НастройкиМаршрутизации.Получить(Идентификатор);
		
		Коммит.Вставить("settings", Настройка);
		
	КонецЦикла;
	
КонецПроцедуры

// Подготавливает необходимую информацию о файлах настроек для получения их через HTTPЗапрос методом "GET".
//
// Параметр:
// 	ДанныеТелаЗапроса - (См. ПолучитьДанныеТелаЗапроса.ДанныеТелаЗапроса).
//	ПараметрыПроекта - (См. ПараметрыПроектаИзТелаЗапроса)
// Возвращаемое значение:
// 	Соответствие - список коммитов и описание файлов настроек:
//	* Ключ - Строка - идентификатор коммита.
//	* Значение - Структура - описание файла:
//		** ПутьКФайлу 	 - Строка - относительный путь к файлу
//			в формате "/api/v4/projects/%1/repository/files/%2/raw?ref=%3"
Функция ОписаниеФайловНастроекМаршрутизации(Знач ДанныеТелаЗапроса, Знач ПараметрыПроекта)
	
	Перем ИмяФайлаНастроек;
	Перем ИдентификаторПроекта;
	Перем ИдентификаторКоммита;
	Перем ПутьКФайлу;
	
	Перем Результат;
	
	ИмяФайлаНастроек     = ИмяФайлаНастроекМаршрутизации();
	ИдентификаторПроекта = ПараметрыПроекта.Идентификатор;
	
	Результат = Новый Соответствие;
	
	Для каждого Коммит Из ДанныеТелаЗапроса.Получить("commits") Цикл
		
		ИдентификаторКоммита = Коммит.Получить("id");
		ПутьКФайлу = СформироватьОтносительныйПутьКФайлуRAW(ИдентификаторПроекта, ИдентификаторКоммита, ИмяФайлаНастроек);
		ОписаниеФайлов = Новый Структура;
		ОписаниеФайлов.Вставить("ПутьКФайлу", ПутьКФайлу);
		Результат.Вставить(ИдентификаторКоммита, ОписаниеФайлов);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

//TODO дописать описание после окончания формирования структуры настроек
//
// Получает структуру настроек маршрутизации файлов внешних отчетов/обработок для каждого коммита.
// 
// Параметры:
//	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
// 	ОписаниеНастроек - Соответствие - описание файлов настроек:
//	* Ключ - Строка - идентификатор коммита.
//	* Значение - Структура - описание файла:
//		** ПутьКФайлу - Строка - относительный путь к файлу
//			в формате "/api/v4/projects/%1/repository/files/%2/raw?ref=%3"
// 	PrivateTokenGitLab - (См. PrivateTokenGitLab) 
//	Таймаут - (См. ТаймаутGitLab)
// Возвращаемое значение:
// 	Соответствие - список коммитов и настройки маршрутизации:
//	* Ключ - Строка - идентификатор коммита.
//	* Значение - Структура - настройки маршрутизации:
//  	** ???         - Строка - схема из URI.
//   	** ???         - Строка - логин из URI.
Функция НастройкиМаршрутизации(Знач ИдентификаторWebhook,
									Знач ОписаниеНастроек,
									Знач ПараметрыПроекта,
									Знач PrivateTokenGitLab,
									Знач Таймаут = 0)
	
	Перем СтруктураURI;
	Перем HTTPОтвет;
	Перем НастройкиИзФайла;
	Перем НастройкиМаршрутизации;
	
	СтруктураURI = ПараметрыПроекта.СтруктураURI;
	
	НастройкиМаршрутизации = Новый Соответствие;
	Для каждого ОписаниеФайла Из ОписаниеНастроек Цикл
		
		HTTPОтвет = ВыполнитьHTTPЗапросНаПолучениеФайла(ИдентификаторWebhook,
		                                                СтруктураURI,
														ОписаниеФайла.Значение.ПутьКФайлу,
														PrivateTokenGitLab,
														Таймаут);
		
		НастройкиИзФайла = Неопределено;
		Если HTTPОтвет.КодСостояния = 200 Тогда
			РаботаСИнтернетСервисами.ТелоHTTPОтветаВКоллекциюКакJSON(HTTPОтвет, Истина, Истина, НастройкиИзФайла);
		КонецЕсли;
		
		НастройкиМаршрутизации.Вставить(ОписаниеФайла.Ключ, НастройкиИзФайла);
		
	КонецЦикла;
	
	Возврат НастройкиМаршрутизации;
	
КонецФункции

// Получает значение константы с именем файла настроек маршрутизации, расположенном в корне репозитория GitLab.
//
// Параметры:
// Возвращаемое значение:
// 	Строка - возвращает имя файла (макс. 50), при незаполненном значении вызывается исключение.
Функция ИмяФайлаНастроекМаршрутизации()
	
	Перем ИмяФайлаНастроекМаршрутизации;
	
	ИмяФайлаНастроекМаршрутизации = Константы.ИмяФайлаНастроекМаршрутизацииGitLab.Получить();
	ОбщегоНазначенияКлиентСервер.Проверить(НЕ ПустаяСтрока(ИмяФайлаНастроекМаршрутизации),
		НСтр("ru = 'Настройка ""Имя файла настроек маршрутизации"" должна быть заполнена.'"),
		"СервисыGitLab.ИмяФайлаНастроекМаршрутизации");
	
	Возврат ИмяФайлаНастроекМаршрутизации;
	
КонецФункции

#КонецОбласти

#Область Авторизация

// Получает значение константы с private token пользователя GitLab, имеющего доступ к api GitLab.
// 
// Параметры:
// Возвращаемое значение:
// 	Строка - возвращает значение PRIVATE-TOKEN (макс. 50), при незаполненном значении вызывается исключение. 
Функция GitLabUserPrivateToken()
	
	Перем GitLabUserPrivateToken;
	
	GitLabUserPrivateToken = Константы.GitLabUserPrivateToken.Получить();
	ОбщегоНазначенияКлиентСервер.Проверить(НЕ ПустаяСтрока(GitLabUserPrivateToken),
		НСтр("ru = 'Настройка ""GitLab user private token"" должна быть заполнена.'"),
		"СервисыGitLab.GitLabUserPrivateToken");
	
	Возврат GitLabUserPrivateToken;
	
КонецФункции


// Получает значение константы AccessTokenReceiver, используемое для подключения к сервисам
// конечных точек доставки файлов.
// 
// Параметры:
// Возвращаемое значение:
// 	Строка - возвращает AccessTokenReceiver подключения к базам-получателям (макс. 20);
// 
Функция AccessTokenReceiver()
	
	Перем Результат;
	
	Результат = Константы.AccessTokenReceiver.Получить();
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область НастройкиСервисов

// Получает значение константы с таймаутом соединения к ИБ-получателю.
//
// Параметры:
// Возвращаемое значение:
// 	Число - секунды, если 0 - таймаут не устанавливается, макс. - 3600 сек.;
Функция ТаймаутИБПолучателя()
	Возврат ТаймаутЗапросов("ТаймаутИБПолучателя");
КонецФункции

// Получает значение константы с таймаутом соединения к серверу GitLab.
//
// Параметры:
// Возвращаемое значение:
// 	Число - секунды, если 0 - таймаут не устанавливается, макс. - 3600 сек.;
Функция ТаймаутGitLab()
	Возврат ТаймаутЗапросов("ТаймаутGitLab");
КонецФункции

// Получает значение таймаута соединения из константы, имя которой передается в качестве параметра;
// 
// Параметры:
// 	Параметр - Строка - имя константы, в которой хранится значение таймаута;
// Возвращаемое значение:
// 	Число - секунды, если 0 - таймаут не устанавливается, макс. - 3600 сек.;
Функция ТаймаутЗапросов(Знач Параметр)
	
	Перем Таймаут;
	Перем Результат;

	Таймаут = ?(Метаданные.Константы.Найти(Параметр) = Неопределено, 0, Константы[Параметр].Получить());
	
	Результат = 0;
	Если Таймаут = Неопределено Тогда
		Результат = 0;
	ИначеЕсли Таймаут > 3600 Тогда
		Результат = 3600;
	Иначе
		Результат = Таймаут;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

// Сохраняет "исходные данные HTTP-запроса" или "обрабатываемые файлы" (См. РегистрСведений.ОбработчикиСобытий).
// 
// Параметры:
// 	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
// 	Ключ - Строка - значение checkout_sha коммита;
// 	Ресурс - Строка - имя ресурса, в который записываются данные;
// 	Данные - Соответствие - любое значение, сохраняется в формате ХранилищеЗначения;
Процедура СохранитьДанныеВИБ(Знач ИдентификаторWebhook, Знач Ключ, Знач Ресурс, Знач Данные)
	
	Перем ТекстСообщения;
	
	Перем Блокировка;
	Перем ЭлементБлокировки;
	Перем ПараметрыКлюча;
	Перем КлючЗаписи;
	Перем ХранилищеЗначенияДанные;
	Перем ЗаписываемыеДанные;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки       = Блокировка.Добавить("РегистрСведений.ДанныеОбработчиковСобытий");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	ЭлементБлокировки.УстановитьЗначение("Идентификатор", ИдентификаторWebhook);
	ЭлементБлокировки.УстановитьЗначение("Ключ", Ключ);
	Блокировка.Заблокировать();

	НачатьТранзакцию();	
	Попытка
		ПараметрыКлюча = Новый Структура;
		ПараметрыКлюча.Вставить("Идентификатор", ИдентификаторWebhook);
		ПараметрыКлюча.Вставить("Ключ", Ключ);
		КлючЗаписи = РегистрыСведений.ДанныеОбработчиковСобытий.СоздатьКлючЗаписи(ПараметрыКлюча);
		ХранилищеЗначенияДанные = Новый ХранилищеЗначения(Данные);
		ЗаписываемыеДанные = Новый Структура(Ресурс, ХранилищеЗначенияДанные);
		РегистрыСведений.ДанныеОбработчиковСобытий.ЗаписатьЗначенияРесурсов(КлючЗаписи, ЗаписываемыеДанные); 
		ЗафиксироватьТранзакцию();
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		ТекстСообщения = НСтр("ru = 'Не удалось сохранить данные в ИБ для ресурса: %1.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Ресурс);
		
		ДополнительныеПараметры = Логирование.ДополнительныеДанные();
		ДополнительныеПараметры.Объект = ИдентификаторWebhook;
		Логирование.Предупреждение( "Core.СохранениеДанных", ТекстСообщения, ДополнительныеПараметры );
		
		Возврат;
	
	КонецПопытки;
	
КонецПроцедуры

#Область ФоновыеЗадания

Процедура ФоноваяОбработкаДанныхЗапроса(Знач ИдентификаторWebhook,
										Знач КлючЗапросаGitLab,
										Знач ДанныеТелаЗапроса,
										Знач ПараметрыПроектаРепозитория,
										Знач ЭтоРучнойЗапуск = Ложь) Экспорт
	
	Перем ПараметрыЗапроса;
	Перем ДанныеДляОтправки;
	Перем ПараметрыДоставки;
	Перем КлючОтправкиФайла;
	Перем ТекстСообщения;
	Перем ДополнительныеПараметры;
	
	ДополнительныеПараметры = Логирование.ДополнительныеДанные();
	ДополнительныеПараметры.Объект = ИдентификаторWebhook;
	
	Попытка
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("КлючЗапросаGitLab", КлючЗапросаGitLab);
		ПараметрыЗапроса.Вставить("ПараметрыПроектаРепозитория", ПараметрыПроектаРепозитория);
		ПараметрыЗапроса.Вставить("ДанныеТелаЗапроса", ДанныеТелаЗапроса);
		ПараметрыЗапроса.Вставить("ЭтоРучнойЗапуск", ЭтоРучнойЗапуск);
		ПараметрыЗапроса = Новый ФиксированнаяСтруктура(ПараметрыЗапроса);
		
		ДанныеДляОтправки = ПодготовитьДанныеКОтправке(ИдентификаторWebhook, ПараметрыЗапроса);
		
		Если ДанныеДляОтправки.Количество() = 0 Тогда
			
			ТекстСообщения = НСтр("ru = '%1: нет данных для отправки. Задание завершено.'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, КлючЗапросаGitLab);
			Логирование.Информация( "Core.ОбработкаДанныхВФоне", ТекстСообщения, ДополнительныеПараметры );
			
			Возврат;
			
		КонецЕсли;
		
		ПараметрыДоставки = ИнициализироватьПараметрыДоставки();
//		ПараметрыДоставки.Пользователь = ПользовательИБПолучателя();
		ПараметрыДоставки.Token       = AccessTokenReceiver();
		ПараметрыДоставки.Таймаут      = ТаймаутИБПолучателя();
		
		Для каждого ТочкаДоставки Из ДанныеДляОтправки Цикл
			
			ПараметрыДоставки.ТочкаДоставки = ТочкаДоставки.Адрес;
			
			КлючОтправкиФайла = КлючЗапросаGitLab + "|" + ТочкаДоставки.ИмяФайла + "|" + ТочкаДоставки.Адрес;
			
			ПараметрыЗадания = Новый Массив;
			ПараметрыЗадания.Добавить(ИдентификаторWebhook);
			ПараметрыЗадания.Добавить(КлючЗапросаGitLab);
			ПараметрыЗадания.Добавить(ТочкаДоставки.ИмяФайла);
			ПараметрыЗадания.Добавить(ТочкаДоставки.ДвоичныеДанные);
			ПараметрыЗадания.Добавить(ПараметрыДоставки);
			
			// В 8.3.14 реализован иной способ ожидания выполнения заданий, возможен пересмотр обработки исключений.
			Попытка
				
				АктивныеЗадания = АктивныеЗадачиПоКлючу(КлючОтправкиФайла);
				
				Если ЗначениеЗаполнено(АктивныеЗадания) Тогда
					
					ТекстСообщения = НСтр("ru = '%1: Задание отправки файла уже было запущено и активно: КлючФайла: %2'");
					ТекстСообщения = СтрШаблон(ТекстСообщения, КлючЗапросаGitLab, КлючОтправкиФайла);
					Логирование.Предупреждение( "Core.ПередачаФайлаВИБПриемник", ТекстСообщения, ДополнительныеПараметры );
					
					Продолжить;
					
				КонецЕсли;
				
				ЗаданиеОтправкаФайла = ФоновыеЗадания.Выполнить("ПередачаДанных.Отправить",
																ПараметрыЗадания,
																КлючОтправкиФайла,
																"СервисыGitLab: " + КлючЗапросаGitLab);
			Исключение
				
				ТекстСообщения = НСтр("ru = '%1: UUID: %2; %3'");
				ТекстСообщения = СтрШаблон(ТекстСообщения,
										   КлючЗапросаGitLab,
										   ЗаданиеОтправкаФайла.УникальныйИдентификатор,
										   ИнформацияОбОшибке().Описание);
										   
				Логирование.Ошибка("Core.ПередачаФайлаВИБПриемник", ТекстСообщения, ДополнительныеПараметры );
				
			КонецПопытки;
			
		КонецЦикла;
		
		ТекстСообщения = НСтр("ru = '%1: количество файлов к отправке: %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, КлючЗапросаGitLab, ДанныеДляОтправки.Количество());
		Логирование.Информация("Core.ОбработкаДанныхВФоне.Окончание", ТекстСообщения, ДополнительныеПараметры );
		
	Исключение
		
		ТекстСообщения = НСтр("ru = '%1: %2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, КлючЗапросаGitLab, ИнформацияОбОшибке().Описание);
		Логирование.Ошибка("Core.ОбработкаДанныхВФоне", ТекстСообщения, ДополнительныеПараметры);
		
	КонецПопытки;
	
КонецПроцедуры

Функция АктивныеЗадачиПоКлючу(Знач Ключ)
	
	Перем ПараметрыОтбора;
	Перем ВсеФоновыеЗадания;
	Перем АктивныеИсполнители;
	
	ПараметрыОтбора = Новый Структура("Ключ, Состояние", Ключ, СостояниеФоновогоЗадания.Активно);
	ВсеФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(ПараметрыОтбора);
	АктивныеИсполнители = Новый Массив;
	Для каждого Фоновое Из ВсеФоновыеЗадания Цикл
		Если Фоновое.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			АктивныеИсполнители.Добавить(Фоновое);
		КонецЕсли;
	КонецЦикла;
	
	Возврат АктивныеИсполнители;
	
КонецФункции

#КонецОбласти


#Область Файлы

// Ищет в таблице изменений файлов репозитория измененные двоичные файлы и подготавливает новую таблицу
// для получения двоичных данных с сервера GitLab.
// 
// Параметры:
// 	ИзмененияВРепозитории - (См. ВсеДействияНадДвоичнымиФайламиВнешнегоХранилища)
// 	Коммит - Строка - идентификатор коммита.
// Возвращаемое значение:
//	- ТаблицаЗначений - описание изменений:
// 		* Коммит - Строка - идентификатор коммита;
// 		* Дата - Дата - дата коммита;
// 		* ПолноеИмяФайла - Строка - имя файла вместе с директориями, по которому осуществлялась операция;
// 		* ИмяФайла - Строка - имя файла;
// 		* ПутьКФайлу - Строка - относительный путь к файлу в формате URL;
// 		* Операция - Строка - вид операции над файлом (A - добавлен, D - удален, M - изменен);
//		* ДвоичныеДанные - ДвоичныеДанные - тело файла.
Функция НайтиИзмененныеФайлы(Знач ИзмененияВРепозитории, Знач Коммит)
	
	Перем ПараметрыОтбора;
	Перем НайденныеСтроки;
	Перем Результат;
	
	ПараметрыОтбора = Новый Структура("Коммит, Операция", Коммит, "M");
	НайденныеСтроки = ИзмененияВРепозитории.НайтиСтроки(ПараметрыОтбора);
	
	Результат = ИзмененияВРепозитории.Скопировать(НайденныеСтроки);
	
	Возврат Результат;
	
КонецФункции

// По расширению файла определяет является ли файл скомплилированным вариантом внешнего отчета или обработки.
// 
// Параметры:
// 	ПолноеИмяФайла - Строка - имя файла вместе с директориями;
// Возвращаемое значение:
// 	Булево - Истина - это скомпилированный вариант файла, иначе - Ложь.
Функция ЭтоИмяСкомпилированногоФайла(Знач ПолноеИмяФайла = "")
	Возврат (СтрЗаканчиваетсяНа(ПолноеИмяФайла, ".epf") ИЛИ СтрЗаканчиваетсяНа(ПолноеИмяФайла, ".erf"));
КонецФункции

// Формирует относительный путь к файлу в RAW формате в кодировке URL. 
// 
// Параметры:
// 	ИдентификаторПроекта - Строка - идентификатор (id) проекта;
// 	Коммит - Строка - идентификатор коммита;
// 	ПолноеИмяФайла - Строка - имя файла вместе с директориями;
// Возвращаемое значение:
// 	Строка - относительный путь к файлу.
Функция СформироватьОтносительныйПутьКФайлуRAW(Знач ИдентификаторПроекта, Знач Коммит, Знач ПолноеИмяФайла)
	
	Перем Шаблон;
	
	Шаблон     = "/api/v4/projects/%1/repository/files/%2/raw?ref=%3";
	ПолноеИмяФайла   = КодироватьСтроку(ПолноеИмяФайла, СпособКодированияСтроки.КодировкаURL);
	
	Возврат СтрШаблон(Шаблон, ИдентификаторПроекта, ПолноеИмяФайла, Коммит);
	
КонецФункции

// Получение списка имен файлов по конкретному виду операции над файлом (был добавлен, удален или модифицирован).
// 
// Параметры:
// 	Коммит - Соответствие - свойства коммита из преобразованного в Соответствие данных тела запроса.
// 		(См. ПолучитьДанныеТелаЗапроса.ДанныеТелаЗапроса)
// 	ВидОперации - Строка - вид операции над файлом (См. ОбозначениеОперацийНадФайламиВGit).
// Возвращаемое значение:
// 	Массив из Строка - массив имен бинарных файлов (с расширением epf или erf). 
Функция ПолныеИменаСкомпилированныхФайловПоВидуОперации(Знач Коммит, Знач ВидОперации)
	
	Перем ИменаФайловВсехТипов;
	Перем Результат;
	
	ИменаФайловВсехТипов = Коммит.Получить(ВидОперации);
	
	Результат = Новый Массив;

	Для каждого ПолноеИмяФайла Из ИменаФайловВсехТипов Цикл
		Если ЭтоИмяСкомпилированногоФайла(ПолноеИмяФайла) Тогда
			Результат.Добавить(ПолноеИмяФайла);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Соответсвие между видом действий над файлами из данных тела запроса и кратким обозначением этих действий в git.
// 
// Параметры:
// Возвращаемое значение:
// 	Соответствие - описание:
//	* Ключ - Строка - обозначение действия в данных тела запроса;
//	* Значение - Строка - краткое обозначение действия в git.
Функция ОбозначениеОперацийНадФайламиВGit()
	
	Результат = Новый Соответствие;
	Результат.Вставить("added", "A");
	Результат.Вставить("modified", "M");
	Результат.Вставить("removed", "D");
	
	Возврат Результат;
	
КонецФункции

//TODO smelt во входящих, сократить количество
// Выполняет HTTPЗапрос по методу "GET" к серверу для получения файла. 
// 
// Параметры:
//	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
// 	СтруктураURI - Структура - параметры файла:
// 	* Схема         - Строка - схема из URI.
//  * Логин         - Строка - логин из URI.
//  * Пароль        - Строка - пароль из URI.
//  * ИмяСервера    - Строка - часть <хост>:<порт> из URI.
//  * Хост          - Строка - хост из URI.
//  * Порт          - Строка - порт из URI.
//  * ПутьНаСервере - Строка - часть <путь>?<параметры>#<якорь> из URI.
//	ПутьКФайлу 	 - Строка - относительный путь к файлу
//		в формате "/api/v4/projects/%1/repository/files/%2/raw?ref=%3"
// 	PrivateTokenGitLab - (См. PrivateTokenGitLab)
// 	Таймаут - Число - таймаут осуществляемого соединения и операций, в секундах. 0 - не устанавливать таймаут.
// Возвращаемое значение:
// 	HTTPОтвет - HTTPОтвет - HTTP-ответ сервера на запрос получения файла.
Функция ВыполнитьHTTPЗапросНаПолучениеФайла(Знач ИдентификаторWebhook,
												Знач СтруктураURI,
												Знач ПутьКФайлу,
												Знач PrivateTokenGitLab,
												Знач Таймаут = 0) Экспорт
	
	Перем Соединение;
	Перем HTTPЗапрос;
	Перем HTTPОтвет;
	
	Соединение = Новый HTTPСоединение(СтруктураURI.ИмяСервера, , , , , Таймаут);
	
	//TODO mock
	//Соединение = Новый HTTPСоединение("8879f2e6-33c1-48ba-9bfa-93c75847bd7c.mock.pstmn.io", , , , , Таймаут);
	//TODO mock
	
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = ПутьКФайлу;
	HTTPЗапрос.Заголовки.Вставить("PRIVATE-TOKEN", PrivateTokenGitLab);
	
	Попытка
		HTTPОтвет = Соединение.ВызватьHTTPМетод("GET", HTTPЗапрос);
	Исключение
		
		ДополнительныеПараметры = Логирование.ДополнительныеДанные();
		ДополнительныеПараметры.Объект = ИдентификаторWebhook;
		ТекстСообщения = НСтр("ru = 'Ошибка при вызове метода GET: %1%2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, СтруктураURI.ИмяСервера, ПутьКФайлу);
		Логирование.Ошибка( "Core.ПолучениеДанныхДляОтправкиФайлов", ТекстСообщения, ДополнительныеПараметры );
	    //ВызватьИсключение;
	КонецПопытки;
	
	Возврат HTTPОтвет;
	
КонецФункции

// Перечень операций над файлами в репозитории, по которым необходимо формировать таблицу изменений.
//  (См. ВсеДействияНадДвоичнымиФайламиВнешнегоХранилища).
// Параметры:
// Возвращаемое значение:
// 	Массив из Строка - массив строк с видами операций над файлами в репозитории.
Функция ОбрабатываемыеВидыОперацийНадФайлами()
	
	Перем Результат;
	Перем ОперацииНадФайлами;
	
	ОперацииНадФайлами = ОбозначениеОперацийНадФайламиВGit();
	Результат = Новый Массив;
	//Результат.Добавить("modified");
	Для каждого Операция Из ОперацииНадФайлами Цикл
		Результат.Добавить(Операция.Ключ);
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РаботаСДанными

// Отбирает к отправке только последние по времени модификации версии файлов.
// 
// Параметры:
// 	ТаблицаЗначений - (См. ОписаниеТаблицыДанныхДляОтправки)
// Возвращаемое значение:
// 	(См. ОписаниеТаблицыДанныхДляОтправки)
Функция ВыбратьТолькоПоследниеИзменения(Знач ТаблицаЗначений)
	
	Перем Результат;
	Перем Копия;
	Перем ПараметрыОтбора;
	Перем НайденныеСтроки;
	
	Результат        = ТаблицаЗначений.СкопироватьКолонки();
	МассивИменФайлов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТаблицаЗначений.ВыгрузитьКолонку("ПолноеИмяФайла"));
	
	Если НЕ ЗначениеЗаполнено(МассивИменФайлов) Тогда
		
		Возврат Результат;
	
	КонецЕсли;
	
	Копия = ТаблицаЗначений.Скопировать();
	Копия.Сортировать("Дата Убыв");
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ПолноеИмяФайла",);
	
	Для каждого ИмяФайла Из МассивИменФайлов Цикл
		
		ПараметрыОтбора.ПолноеИмяФайла = ИмяФайла;
		НайденныеСтроки = Копия.НайтиСтроки(ПараметрыОтбора);
		ЗаполнитьЗначенияСвойств(Результат.Добавить(), НайденныеСтроки[0]);
		
	КонецЦикла;
	
	Копия = Неопределено;
	
	Возврат Результат;
	
КонецФункции

// Получает параметры проекта репозитория из преобразованных в Соответствие данных тела запроса.
// 
// Параметры:
// 	ДанныеТелаЗапроса - (См. ПолучитьДанныеТелаЗапроса.ДанныеТелаЗапроса).
// Возвращаемое значение:
// 	Структура - Описание:
// * Идентификатор - Строка - идентификатор проекта.
// * СтруктураURI - (См. ОбщегоНазначенияКлиентСервер.СтруктураURI). 
Функция ПараметрыПроектаИзТелаЗапроса(Знач ДанныеТелаЗапроса)
	
	Перем ОписаниеПроекта;
	Перем ВебАдрес;
	Перем Идентификатор;
	Перем СтруктураURI;
	
	Перем Результат;
	
	Результат = Новый Структура;
	
	Если ДанныеТелаЗапроса = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОписаниеПроекта = ДанныеТелаЗапроса.Получить("project");
	
	ВебАдрес      = ОписаниеПроекта.Получить("web_url");
	Идентификатор = Строка(ОписаниеПроекта.Получить("id"));
	Результат.Вставить("Идентификатор", Идентификатор);
	
	// TODO перепроверить вызов
	СтруктураURI = КоннекторHTTP.РазобратьURL(ВебАдрес);
	Результат.Вставить("СтруктураURI", Новый ФиксированнаяСтруктура(СтруктураURI));
	
	Возврат Результат;
	
КонецФункции

Функция ПодготовитьДанныеКОтправке(Знач ИдентификаторWebhook, Знач Параметры)
	
	Перем КлючЗапроса;
	Перем ДанныеТелаЗапроса;
	Перем Таймаут;
	Перем ТочкиДоставки;
	Перем НовыйКлючЗаписи;
	Перем ДанныеДляОтправки;
	Перем ТекстСообщения;
	
	ДанныеТелаЗапроса = Параметры.ДанныеТелаЗапроса;
	Таймаут = ТаймаутGitLab();

	ДополнительныеПараметры = Логирование.ДополнительныеДанные();
	ДополнительныеПараметры.Объект = ИдентификаторWebhook;	

	ТекстСообщения = НСтр("ru = '%1: подготовка данных к отправке.'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, Параметры.КлючЗапросаGitLab);

	Логирование.Информация("Core.ПодготовкаДанных.Начало", ТекстСообщения, ДополнительныеПараметры);

	Если Параметры.ЭтоРучнойЗапуск Тогда
		НовыйКлючЗаписи = Новый Структура("Идентификатор, Ключ", ИдентификаторWebhook, Параметры.КлючЗапросаGitLab);
		КлючЗапроса = РегистрыСведений.ДанныеОбработчиковСобытий.СоздатьКлючЗаписи(НовыйКлючЗаписи);
		ДанныеДляОтправки = РегистрыСведений.ДанныеОбработчиковСобытий.ПолучитьДанныеОтправки(КлючЗапроса);
	Иначе
		ДополнитьДанныеЗапросаНастройками(ИдентификаторWebhook,
												Параметры.ПараметрыПроектаРепозитория,
												Таймаут,
												ДанныеТелаЗапроса);
		СервисыGitLab.СохранитьДанныеТелаЗапросаВИБ(ИдентификаторWebhook, Параметры.КлючЗапросаGitLab, ДанныеТелаЗапроса);
		ДанныеДляОтправки = ДанныеДляОтправки(ИдентификаторWebhook,
														 ДанныеТелаЗапроса,
														 Параметры.ПараметрыПроектаРепозитория,
														 Таймаут);
		
		СервисыGitLab.СохранитьДвоичныеДанныеВИБ(ИдентификаторWebhook, Параметры.КлючЗапросаGitLab, ДанныеДляОтправки);
	КонецЕсли;
	
	ТочкиДоставки     = ТочкиДоставкиФайлов(ИдентификаторWebhook, ДанныеТелаЗапроса, ДанныеДляОтправки);
	ДанныеДляОтправки = РаспределитьДанныеПоМаршрутам(ДанныеДляОтправки, ТочкиДоставки);
	
	ТекстСообщения = НСтр("ru = '%1: подготовка данных к отправке.'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, Параметры.КлючЗапросаGitLab);
	Логирование.Информация("Core.ПодготовкаДанных.Окончание", ТекстСообщения, ДополнительныеПараметры );
	
	Возврат ДанныеДляОтправки;
	
КонецФункции

// Распределяет двоичные данные для отправки по точкам доставки.
//
// Параметры:
// 	ДанныеДляОтправки - (См. ОписаниеТаблицыДанныхДляОтправки);
// 	ТочкиДоставки - (См. ТочкиДоставкиФайловИзДанныхЗапроса);
// Возвращаемое значение:
// 	(См. ОписаниеТаблицыДанныхДляОтправки).
Функция РаспределитьДанныеПоМаршрутам(Знач ДанныеДляОтправки, Знач ТочкиДоставки)
	
	Перем ПараметрыОтбора;
	Перем НайденныеДвоичныеДанные;
	Перем ДвоичныеДанные;
	Перем НоваяСтрока;
	Перем Результат;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Коммит", );
	ПараметрыОтбора.Вставить("ПолноеИмяФайла", );
	
	Результат = ТочкиДоставки.СкопироватьКолонки();
	
	Для каждого ТочкаДоставки Из ТочкиДоставки Цикл
		
		ПараметрыОтбора.Коммит   		= ТочкаДоставки.Коммит;
		ПараметрыОтбора.ПолноеИмяФайла 	= ТочкаДоставки.ПолноеИмяФайла;
		
		НайденныеДвоичныеДанные = ДанныеДляОтправки.НайтиСтроки(ПараметрыОтбора);
		
		Если ЗначениеЗаполнено(НайденныеДвоичныеДанные) Тогда
			
			ДвоичныеДанные = НайденныеДвоичныеДанные[0].ДвоичныеДанные;
			
			Если ДвоичныеДанные <> Неопределено Тогда
				
				НоваяСтрока = Результат.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТочкаДоставки);
				НоваяСтрока.ДвоичныеДанные = ДвоичныеДанные;
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Заполняет таблицу изменений репозитория необходимыми данными, по которым с сервера GitLab будут забираться
// двоичные данные внешних отчетов/обработок.
// 
// Параметры:
// 	ДанныеТелаЗапроса - (См. ПолучитьДанныеТелаЗапроса.ДанныеТелаЗапроса).
// 	ПараметрыПроекта - (См. ПараметрыПроектаИзТелаЗапроса)
// Возвращаемое значение:
// 	ТаблицаЗначений - Описание:
// * Коммит - Строка - идентификатор коммита.
// * Дата - Дата - дата коммита.
// * ПолноеИмяФайла - Строка - имя файла вместе с директориями, по которому осуществлялась операция.
// * ПутьКФайлу - Строка - относительный путь к файлу в формате URL
// * Операция - Строка - вид операции над файлом (A - добавлен, D - удален, M - изменен).
Функция ВсеДействияНадДвоичнымиФайламиВнешнегоХранилища(Знач ДанныеТелаЗапроса, Знач ПараметрыПроекта)
	
	Перем Коммиты;
	Перем ВидыОперацийНадФайлами;
	Перем ПараметрыКоммита;
	Перем ПараметрыЗаполнения;
	Перем ПолныеИменаФайлов;
	Перем ТаблицаИзменений;
	
	Коммиты = ДанныеТелаЗапроса.Получить("commits");
	ВидыОперацийНадФайлами = ОбрабатываемыеВидыОперацийНадФайлами();
	ТаблицаИзменений       = ОписаниеТаблицыДанныхДляОтправки();
	
	Для каждого Коммит Из Коммиты Цикл
		
		ПараметрыКоммита = Новый Структура;
		ПараметрыКоммита.Вставить("Идентификатор", Коммит.Получить("id"));
		ПараметрыКоммита.Вставить("Дата", Коммит.Получить("timestamp"));
		ПараметрыЗаполнения = Новый ФиксированнаяСтруктура("Коммит, Проект", ПараметрыКоммита, ПараметрыПроекта);
		
		Для каждого ВидОперации Из ВидыОперацийНадФайлами Цикл
			
			ПолныеИменаФайлов = ПолныеИменаСкомпилированныхФайловПоВидуОперации(Коммит, ВидОперации);
			ДополнитьТаблицуОтправкиДанныхИзменениямиРепозитория(ПолныеИменаФайлов,
																	ВидОперации,
																	ПараметрыЗаполнения,
																	ТаблицаИзменений);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаИзменений;
	
КонецФункции

// Подготавливает описание файлов и их содержимое для отправки данных в ИБ-приемник.
// 
// Параметры:
//	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
// 	ДанныеТелаЗапроса - (См. ПолучитьДанныеТелаЗапроса.ДанныеТелаЗапроса)
//	ПараметрыПроекта - (См. ПараметрыПроектаИзТелаЗапроса)
//	Таймаут - (См. ТаймаутGitLab);
// Возвращаемое значение:
//	(См. ОписаниеТаблицыДанныхДляОтправки)
Функция ДанныеДляОтправки(Знач ИдентификаторWebhook, Знач ДанныеТелаЗапроса, Знач ПараметрыПроекта, Знач Таймаут = 0)
	
	Перем ИзмененияВРепозитории;
	Перем PrivateToken;
	Перем Коммиты;
	Перем Идентификатор;
	Перем ДанныеДляОтправки;
	Перем Результат;
	
	ИзмененияВРепозитории = ВсеДействияНадДвоичнымиФайламиВнешнегоХранилища(ДанныеТелаЗапроса, ПараметрыПроекта);
	ИзмененияВРепозитории = ВыбратьТолькоПоследниеИзменения(ИзмененияВРепозитории);
	PrivateToken          = GitLabUserPrivateToken();
	
	Коммиты = ДанныеТелаЗапроса.Получить("commits");
	
	Результат = ИзмененияВРепозитории.СкопироватьКолонки();
	
	Для каждого Коммит Из Коммиты Цикл
		
		Идентификатор     = Коммит.Получить("id");
		ДанныеДляОтправки = ПолучитьДанныеДляОтправкиССервераGitLab(ИдентификаторWebhook,
																		ПараметрыПроекта,
																		ИзмененияВРепозитории,
																		Идентификатор,
																		PrivateToken,
																		Таймаут);
		
		Для Каждого ДанныеДляОтправки Из ДанныеДляОтправки Цикл
			Если ДанныеДляОтправки.ДвоичныеДанные = Неопределено ИЛИ ПустаяСтрока(ДанныеДляОтправки.ИмяФайла) Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Результат.Добавить(), ДанныеДляОтправки);
		КонецЦикла;
		
	КонецЦикла;
	
	ИзмененияВРепозитории = Неопределено;
	
	Возврат Результат;
	
КонецФункции

// Получает двоичные данные с сервера GitLab и помещает их в таблицу с описанием этих файлов.
// 
// Параметры:
//	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
//	ПараметрыПроекта - (См. ПараметрыПроектаИзТелаЗапроса);
// 	ИзмененияВРепозитории - (См. ВсеДействияНадДвоичнымиФайламиВнешнегоХранилища);
// 	Коммит - идентификатор коммита;
// 	PrivateTokenGitLab - (См. PrivateTokenGitLab);
//	Таймаут - (См. ТаймаутGitLab);
// Возвращаемое значение:
//  (См. НайтиИзмененныеФайлы)
Функция ПолучитьДанныеДляОтправкиССервераGitLab(Знач ИдентификаторWebhook,
												Знач ПараметрыПроекта,
												Знач ИзмененияВРепозитории,
												Знач Коммит,
												Знач PrivateTokenGitLab,
												Знач Таймаут = 0)
                                                
	Перем СтруктураURI;
	Перем HTTPОтвет;
	Перем ИмяФайла;
	Перем ДвоичныеДанные;
	Перем ТекстСообщения;
	
	ДанныеДляОтправки = НайтиИзмененныеФайлы(ИзмененияВРепозитории, Коммит);
	СтруктураURI      = ПараметрыПроекта.СтруктураURI;
	
	ДополнительныеПараметры = Логирование.ДополнительныеДанные();
	ДополнительныеПараметры.Объект = ИдентификаторWebhook;	

	Для каждого ОписаниеФайла Из ДанныеДляОтправки Цикл
		
		HTTPОтвет = СервисыGitLab.ВыполнитьHTTPЗапросНаПолучениеФайла(ИдентификаторWebhook,
																	  СтруктураURI,
																	  ОписаниеФайла.ПутьКФайлу,
																	  PrivateTokenGitLab,
																	  Таймаут);
																	  
		ДополнительныеПараметры.HTTPОтвет = HTTPОтвет;
		
		ИмяФайла       = "";
		ДвоичныеДанные = Неопределено;
		
		Если HTTPОтвет.КодСостояния = 200 Тогда
			
			ИмяФайла = HTTPОтвет.Заголовки.Получить("X-Gitlab-File-Name");
			ОписаниеФайла.ИмяФайла = СтроковыеФункцииКлиентСервер.ПерекодироватьСтроку(ИмяФайла, "windows-1251");
			
			ДвоичныеДанные = HTTPОтвет.ПолучитьТелоКакДвоичныеДанные();
			ОписаниеФайла.ДвоичныеДанные = ДвоичныеДанные;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = '%1: с сервера GitLab получен файл %2.'"),
				Коммит,	ОписаниеФайла.ПолноеИмяФайла);
				
			Логирование.Информация( "Core.ПолучениеДанныхДляОтправкиФайлов", ТекстСообщения, ДополнительныеПараметры );
			
		Иначе
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка получения двоичных данных с сервера GitLab: %1%2'"),
				СтруктураURI.ИмяСервера, ОписаниеФайла.ПутьКФайлу);
				
			Логирование.Ошибка( "Core.ПолучениеДанныхДляОтправкиФайлов", ТекстСообщения, ДополнительныеПараметры );
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеДляОтправки;
	
КонецФункции

// Подготавливает таблицу значений с файлами для отправки. 
// 
// Параметры:
// Возвращаемое значение:
// 	ТаблицаЗначений - описание:
// * Коммит - Строка - идентификатор коммита;
// * Дата - Дата - дата коммита;
// * ИмяФайла - Строка - имя файла;
// * ПолноеИмяФайла - Строка - имя файла вместе с директориями;
// * ПутьКФайлу - Строка - относительный путь к файлу в формате URL;
// * Операция - Строка - вид операции над файлом (A - добавлен, D - удален, M - изменен);
// * ДвоичныеДанные - ДвоичныеДанные - тело файла;
Функция ОписаниеТаблицыДанныхДляОтправки()

	Перем ТаблицаЗначений;
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("Коммит", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначений.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаЗначений.Колонки.Добавить("ИмяФайла", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначений.Колонки.Добавить("ПолноеИмяФайла", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначений.Колонки.Добавить("ПутьКФайлу", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначений.Колонки.Добавить("Операция", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначений.Колонки.Добавить("ДвоичныеДанные", Новый ОписаниеТипов("ДвоичныеДанные"));

	Возврат ТаблицаЗначений;
	
КонецФункции

// Подготавливает таблицу точек доставки файлов до сервисов ИБ-получателей.
// 
// Параметры:
// Возвращаемое значение:
// 	ТаблицаЗначений - описание:
// * Коммит - Строка - идентификатор коммита;
// * ИмяФайла - Строка - имя файла;
// * ПолноеИмяФайла - Строка - имя файла вместе с директориями;
// * Адрес - Строка - адрес сервиса ИБ-получателя;
Функция ОписаниеТаблицыТочекДоставкиФайлов()

	Перем ТаблицаЗначений;
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("Коммит", Новый ОписаниеТипов("Строка")); 
	ТаблицаЗначений.Колонки.Добавить("ИмяФайла", Новый ОписаниеТипов("Строка")); 
	ТаблицаЗначений.Колонки.Добавить("ПолноеИмяФайла", Новый ОписаниеТипов("Строка")); 
	ТаблицаЗначений.Колонки.Добавить("Адрес", Новый ОписаниеТипов("Строка"));
	ТаблицаЗначений.Колонки.Добавить("ДвоичныеДанные", Новый ОписаниеТипов("ДвоичныеДанные"));
	
	Возврат ТаблицаЗначений;
	
КонецФункции

// Добавляет в таблицу данные по изменениям репозитория.
// 
// Параметры:
// 	ИменаФайлов - Массив из Строка - массив добавляемых в таблицу изменений имен файлов;
// 	ВидОперации - Строка - вид операции над файлом (См. ОбозначениеОперацийНадФайламиВGit).
// 	Параметры - ФиксированнаяСтруктура - дополнительные параметры:
// * Коммит - Структура  - описание коммита:
// ** Идентификатор - Строка - идентификатор коммита;
// ** Дата - Дата - дата коммита; 
// * Проект - Структура - описание проекта:
// ** Идентификатор - Строка - идентификатор прокта.
// ** СтруктураURI - (См. ОбщегоНазначенияКлиентСервер.СтруктураURI). 
// 	ТаблицаИзменений - ТаблицаЗначений - описание:
// * Коммит - Строка - идентификатор коммита.
// * Дата - Дата - дата коммита.
// * ПолноеИмяФайла - Строка - имя файла вместе с директориями, по которому осуществлялась операция.
// * ПутьКФайлу - Строка - относительный путь к файлу в формате URL
// * Операция - Строка - вид операции над файлом (A - добавлен, D - удален, M - изменен).
Процедура ДополнитьТаблицуОтправкиДанныхИзменениямиРепозитория(Знач ИменаФайлов,
																	Знач ВидОперации,
																	Знач Параметры,
																	ТаблицаИзменений)
	
	Перем НоваяСтрока;
	Перем ПутьКФайлу;
	
	Для каждого ПолноеИмяФайла Из ИменаФайлов Цикл
		
		НоваяСтрока = ТаблицаИзменений.Добавить();
		НоваяСтрока.Коммит         = Параметры.Коммит.Идентификатор;
		НоваяСтрока.Дата           = Параметры.Коммит.Дата;
		НоваяСтрока.ПолноеИмяФайла = ПолноеИмяФайла;
		ПутьКФайлу  = СформироватьОтносительныйПутьКФайлуRAW(Параметры.Проект.Идентификатор,
															Параметры.Коммит.Идентификатор,
															ПолноеИмяФайла);
		НоваяСтрока.ПутьКФайлу = ПутьКФайлу;
		НоваяСтрока.Операция   = ОбозначениеОперацийНадФайламиВGit().Получить(ВидОперации);
		
	КонецЦикла;
	
КонецПроцедуры

// По данным преобразованного в Соответствие тела запроса подготавливает таблицу точек доставки файлов.
//
// Параметры:
// 	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
// 	ДанныеТелаЗапроса - (См. ПолучитьДанныеТелаЗапроса.ДанныеТелаЗапроса)
// Возвращаемое значение:
// 	(См. ОписаниеТаблицыТочекДоставкиФайлов).
Функция ТочкиДоставкиФайлов(Знач ИдентификаторWebhook, Знач ДанныеТелаЗапроса, Знач ДанныеДляОтправки)
	
	Перем Коммиты;
	Перем ТочкиДоставки;
	
	Коммиты       = ДанныеТелаЗапроса.Получить("commits");
	ТочкиДоставки = ОписаниеТаблицыТочекДоставкиФайлов();
	
	ТочкиДоставкиФайловПоВсемИзменениям(ИдентификаторWebhook, ДанныеДляОтправки, Коммиты, ТочкиДоставки);
	СкорректироватьТочкиДоставкиФайлов(ИдентификаторWebhook, Коммиты, ТочкиДоставки);
	
	Возврат ТочкиДоставки;
	
КонецФункции

// Корректирует полный перечень точек доставки файлов применяя правила маршрутизации.
//
// Параметры:
// 	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
// 	Коммиты - Соответствие - (См. ДополнитьДанныеЗапросаНастройками.ДанныеТелаЗапроса)
// 	ТочкиДоставки - (См. ОписаниеТаблицыТочекДоставкиФайлов).
Процедура СкорректироватьТочкиДоставкиФайлов(Знач ИдентификаторWebhook, Знач Коммиты, ТочкиДоставки)
	
	Перем ТекстСообщения;
	Перем ДоступныеСервисыДоставки;
	Перем ИдентификаторКоммита;
	Перем Настройки;
	
	ДополнительныеПараметры = Логирование.ДополнительныеДанные();
	ДополнительныеПараметры.Объект = ИдентификаторWebhook;	
	
	Для каждого Коммит Из Коммиты Цикл
		
		ИдентификаторКоммита = Коммит.Получить("id");
		Настройки            = Коммит.Получить("settings");
		
		Если Настройки = Неопределено Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = '%1: отстутствуют настройки маршрутизации.'"), ИдентификаторКоммита);
			Логирование.Информация("Core.ФормированиеМаршрутовДоставки", ТекстСообщения, ДополнительныеПараметры);
			Продолжить;
			
		КонецЕсли;
		
		ДоступныеСервисыДоставки = ДоступныеСервисыДоставки(Настройки);
		СкорректироватьТочкиДоставкиФайловКоммита(ИдентификаторКоммита, Настройки, ДоступныеСервисыДоставки, ТочкиДоставки);
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует перечень точек доставки файлов на основании правил маршрутизации для определенного коммита.
//
// Параметры:
// 	ИдентификаторКоммита - Строка - идентификатор коммита;
// 	Настройки - Соответствие - (См. ДополнитьДанныеЗапросаНастройками.ДанныеТелаЗапроса)
// 	ДоступныеСервисыДоставки - (См. ДоступныеСервисыДоставки)
// 	ТочкиДоставки - (См. ОписаниеТаблицыТочекДоставкиФайлов).
Процедура СкорректироватьТочкиДоставкиФайловКоммита(Знач ИдентификаторКоммита,
													Знач Настройки,
													Знач ДоступныеСервисыДоставки,
														 ТочкиДоставки)
                                                         
	Перем НастройкиДоставкиФайлов;
	Перем ПолноеИмяФайла;
	Перем Исключения;
	Перем ИсключаемыйАдрес;
	
	НастройкиДоставкиФайлов = Неопределено;
	НастройкиДоставкиФайлов = Настройки.Получить("epf");
	
	Если НастройкиДоставкиФайлов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Коммит", ИдентификаторКоммита);
	ПараметрыОтбора.Вставить("ПолноеИмяФайла",);
	ПараметрыОтбора.Вставить("Адрес",);
	
	Для каждого НастройкаДоставкиФайла Из НастройкиДоставкиФайлов Цикл
		
		ПолноеИмяФайла = НастройкаДоставкиФайла.Получить("file");
		
		Если ПолноеИмяФайла = Неопределено ИЛИ ПустаяСтрока(ПолноеИмяФайла) Тогда
			Продолжить;
		КонецЕсли;
		
		Исключения = НастройкаДоставкиФайла.Получить("exclude");
		
		Если Исключения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого ТекущееИсключение Из Исключения Цикл
			
			ИсключаемыйАдрес = ДоступныеСервисыДоставки.Получить(ТекущееИсключение);
			
			Если ИсключаемыйАдрес <> Неопределено Тогда
				
				ПараметрыОтбора.ПолноеИмяФайла = ПолноеИмяФайла;
				ПараметрыОтбора.Адрес          = ИсключаемыйАдрес;
				
				НайденныеСтроки = ТочкиДоставки.НайтиСтроки(ПараметрыОтбора);
				
				Для каждого УдаляемаяСтрока Из НайденныеСтроки Цикл
					ТочкиДоставки.Удалить(УдаляемаяСтрока);
				КонецЦикла;
			
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует перечень точек доставки файлов для всех изменений без учета правил маршрутизации.
//
// Параметры:
// 	ИдентификаторWebhook - СправочникСсылка.ОбработчикиСобытий - ссылка на описание webhook;
//	ДанныеДляОтправки - (См. ОписаниеТаблицыДанныхДляОтправки)
// 	Коммиты - Соответствие - (См. ДополнитьДанныеЗапросаНастройками.ДанныеТелаЗапроса)
// 	ТочкиДоставки - (См. ОписаниеТаблицыТочекДоставкиФайлов).
Процедура ТочкиДоставкиФайловПоВсемИзменениям(Знач ИдентификаторWebhook,
											  Знач ДанныеДляОтправки,
											  Знач Коммиты,
												   ТочкиДоставки)
                                                   
	Перем ТекстСообщения;
	Перем ДоступныеСервисыДоставки;
	Перем ИдентификаторКоммита;
	Перем Настройки;
	
	ДополнительныеПараметры = Логирование.ДополнительныеДанные();
	ДополнительныеПараметры.Объект = ИдентификаторWebhook;	
	
	Для каждого Коммит Из Коммиты Цикл
		
		ИдентификаторКоммита = Коммит.Получить("id");
		
		Настройки = Коммит.Получить("user_settings");
		
		Если Настройки = Неопределено Тогда
			Настройки = Коммит.Получить("settings");
		КонецЕсли;
		
		Если Настройки = Неопределено Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = '%1: отстутствуют настройки маршрутизации.'"), ИдентификаторКоммита);
			Логирование.Информация( "Core.ФормированиеМаршрутовДоставки", ТекстСообщения, ДополнительныеПараметры );
			Продолжить;
			
		КонецЕсли;
		
		ДоступныеСервисыДоставки = ДоступныеСервисыДоставки(Настройки);
		ТочкиДоставкиФайловПоВсемИзменениямКоммита(ИдентификаторКоммита,
												   ДанныеДляОтправки,
												   ДоступныеСервисыДоставки,
												   ТочкиДоставки);
		
	КонецЦикла;
	
КонецПроцедуры


// Формирует перечень точек доставки файлов для всех изменений без учета правил маршрутизации для определенного коммита.
//
// Параметры:
// 	ИдентификаторКоммита - Строка - индентификатор коммита;
//	ДанныеДляОтправки - (См. ОписаниеТаблицыДанныхДляОтправки)
// 	ДоступныеСервисыДоставки - (См. ДоступныеСервисыДоставки)
// 	ТочкиДоставки - (См. ОписаниеТаблицыТочекДоставкиФайлов).
Процедура ТочкиДоставкиФайловПоВсемИзменениямКоммита(Знач ИдентификаторКоммита,
													 Знач ДанныеДляОтправки,
													 Знач ДоступныеСервисыДоставки,
														  ТочкиДоставки)
														  	
	Перем ПараметрыОтбора;
	Перем НайденныеСтроки;
	Перем НоваяСтрока;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Коммит", ИдентификаторКоммита);
	
	НайденныеСтроки = ДанныеДляОтправки.НайтиСтроки(ПараметрыОтбора);
	
	Для каждого Файл Из НайденныеСтроки Цикл
		
		Для каждого СервисДоставки Из ДоступныеСервисыДоставки Цикл
			
			Если СервисДоставки.Значение = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТочкиДоставки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Файл, , "ДвоичныеДанные");
			НоваяСтрока.Адрес = СервисДоставки.Значение;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// По данным настроек маршрутизации подготавливает список доступных сервисов.
// 
// Параметры:
// 	Настройки - Соответствие - настройки маршрутизации для конкретного коммита.  
// Возвращаемое значение:
// 	Соответствие - доступные сервисы доставки:
//	* Ключ - Строка - имя сервиса
//	* Значение - Строка - адрес сервиса
Функция ДоступныеСервисыДоставки(Знач Настройки)
	
	Перем Результат;
	Перем ВсеСервисыДоставки;
	Перем ИмяСервиса;
	Перем СервисВключен;
	Перем АдресСервиса;
	
	Результат          = Новый Соответствие;
	ВсеСервисыДоставки = Неопределено;
	
	ВсеСервисыДоставки = Настройки.Получить("ws");
	
	Если ВсеСервисыДоставки = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для каждого СервисДоставки Из ВсеСервисыДоставки Цикл
		
		ИмяСервиса = СервисДоставки.Получить("name");
		Если ИмяСервиса = Неопределено ИЛИ ПустаяСтрока(ИмяСервиса) Тогда
			Продолжить;
		КонецЕсли;
		
		СервисВключен = СервисДоставки.Получить("enabled");
		Если СервисВключен <> Неопределено И СервисВключен Тогда
			
			АдресСервиса = СервисДоставки.Получить("address");
			Если АдресСервиса <> Неопределено И НЕ ПустаяСтрока(АдресСервиса) Тогда
				Результат.Вставить(ИмяСервиса, АдресСервиса);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти