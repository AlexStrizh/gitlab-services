#Область ПрограммныйИнтерфейс

// TODO перепроверить описание процедуры

// Возвращает полученный с сервера файл с его описанием.
// 
// Параметры:
// 	URL - Строка - адрес сервера, например, "http://www.gitlab.com";
// 	ПутьКФайлу - Строка - относительный путь к файлу в RAW формате, например,
// 							"/api/v4/projects/1/repository/files/D0%BA%D0%B0%201.epf/raw?ref=ef3529e5486ff";
// 	PrivateTokenGitLab - (См. PrivateTokenGitLab)
// 	Таймаут - (См. ТаймаутGitLab)
// 	
// Возвращаемое значение:
// 	Структура - описание:
// * ИмяФайла - Строка - имя файла, перекодированное из windows-1251;
// * ИмяФайлаИзЗапроса - Строка - имя файла в исходном формате;
// * Данные - ДвоичныеДанные - данные файла;
// * Ошибка - Строка - текст с описанием ошибки получения файла с сервера;
// 
Функция ПолучитьФайл( Знач URL, Знач ПутьКФайлу, Знач PrivateTokenGitLab, Знач Таймаут ) Экспорт

	Перем Адрес;
	Перем Заголовки;
	Перем ДополнительныеПараметры;
	Перем ИмяФайла;
	Перем ИмяФайлаИзЗапроса;
	Перем Ответ;
	Перем Результат;
	
	Адрес = URL + ПутьКФайлу;
	Результат = ОписаниеФайла();

	Попытка

		Заголовки = Новый Соответствие();
		Заголовки.Вставить( "PRIVATE-TOKEN", PrivateTokenGitLab );
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить( "Заголовки", Заголовки );
		ДополнительныеПараметры.Вставить( "Таймаут", Таймаут );
		
		Ответ = КоннекторHTTP.Get( Адрес, Неопределено, ДополнительныеПараметры );

		Если ( НЕ КодыСостоянияHTTPКлиентСерверПовтИсп.isOk(Ответ.КодСостояния) ) Тогда
			
			ВызватьИсключение КодыСостоянияHTTPКлиентСерверПовтИсп.НайтиИдентификаторПоКоду( Ответ.КодСостояния );
		
		КонецЕсли;
		
		ИмяФайлаИзЗапроса = Ответ.Заголовки.Получить( "X-Gitlab-File-Name" );
		
		Если ( ИмяФайлаИзЗапроса = Неопределено ) Тогда
			
			ВызватьИсключение НСтр("ru = 'Файл не найден.'");
			
		КонецЕсли;
		
		ИмяФайла = СтроковыеФункцииКлиентСервер.ПерекодироватьСтроку( ИмяФайлаИзЗапроса, "windows-1251" );
		
		Результат.ИмяФайлаИзЗапроса = ИмяФайлаИзЗапроса;
		Результат.ИмяФайла = ИмяФайла; // windows-1251, иначе будет ошибка веб-сервера 1с
		Результат.Данные = Ответ.Тело;
		
	Исключение
	
		Результат.Ошибка = СтрШаблон( НСтр("ru = 'Ошибка получения файла: %1'"),
										Адрес + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()) );
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает перекодированный в URL относительный путь к файлу в RAW формате. 
// 
// Параметры:
// 	ProjectId - Строка - id проекта;
// 	ПутьКФайлу - Строка - относительный путь к файлу в репозитории (вместе с именем файла);
// 	Commit - Строка - сommit SHA;
// 
// Возвращаемое значение:
// 	Строка - перекодированный в URL относительный путь к файлу.
//
Функция ПутьКФайлуRAW( Знач ProjectId, Знач ПутьКФайлу, Знач Commit ) Экспорт
	
	Перем Шаблон;
	
	Шаблон = "/api/v4/projects/%1/repository/files/%2/raw?ref=%3";
	ПутьКФайлу = КодироватьСтроку( ПутьКФайлу, СпособКодированияСтроки.КодировкаURL );
	
	Возврат СтрШаблон(Шаблон, ProjectId, ПутьКФайлу, Commit);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОписаниеФайла()

	Перем Результат;
	
	Результат = Новый Структура();
	Результат.Вставить( "ИмяФайла" );
	Результат.Вставить( "ИмяФайлаИзЗапроса" );
	Результат.Вставить( "Данные" );
	Результат.Вставить( "Ошибка" );
	
	Возврат Результат;

КонецФункции

#КонецОбласти