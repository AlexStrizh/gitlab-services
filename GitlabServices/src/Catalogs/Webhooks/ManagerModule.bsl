#Region Public

// Поиск элементов справочника по секретному ключу (token), не помеченные на удаление.
//
// Параметры:
// 	Token - Строка - секретный ключ (token);
//
// Returns:
// 	Массив из СправочникСсылка.Webhooks - найденные элементы справочника (пустой массив, если не найдено); 
//
Функция FindByToken( Знач Token ) Экспорт
	
	Var Запрос;
	Var Результат;
	
	Результат = Новый Массив();
	
	Если ( ТипЗнч(Token) <> Тип("Строка") ИЛИ ПустаяСтрока(Token) ) Тогда
		
		Возврат Результат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр( "SecretToken", Token );
	Запрос.Текст = 	"ВЫБРАТЬ
	               	|	Webhooks.Ссылка КАК Ссылка
	               	|ИЗ
	               	|	Catalog.Webhooks КАК Webhooks
	               	|ГДЕ
	               	|	НЕ Webhooks.ПометкаУдаления
	               	|	И Webhooks.SecretToken = &SecretToken";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку( "Ссылка" );
	
КонецФункции

// Загружает в табличную часть объекта данные из журнала регистрации по фильтру.
// 
// Параметры:
// 	Object - СправочникОбъект.Webhooks - объект обработчика событий; 
// 	Destination - Строка - имя табличной части;
// 	Filter - Структура - фильтр отбора для журнала регистрации (См. ГлобальныйКонтекст.ВыгрузитьЖурналРегистрации);
// 	RecordsLoaded - Число - (возвращаемый параметр) количество загруженных записей;
//
Процедура LoadEventsHistory( Object, Val Destination, Val Filter, RecordsLoaded ) Экспорт
	
	EVENT_OBJECT = НСтр( "ru = 'ОбработчикиСобытий';en = 'Webhooks'" );

	Если ТипЗнч(Object) <> Тип("СправочникОбъект.Webhooks") Тогда
		
		Возврат;
		
	КонецЕсли;	

	Object[Destination].Очистить();

	ДанныеЖурналаРегистрации = Новый ТаблицаЗначений();
	ВыгрузитьЖурналРегистрации( ДанныеЖурналаРегистрации, Filter );
	
	Для каждого ЗаписьЖурналаРегистрации Из ДанныеЖурналаРегистрации Цикл
		
		ОписаниеСобытия = ОписаниеСобытияПоЗаписиЖурналаРегистрации( ЗаписьЖурналаРегистрации );
		
		Если ( ОписаниеСобытия.ObjectName <> EVENT_OBJECT ) Тогда
			
			Продолжить;
			
		КонецЕсли;

		НоваяЗаписьИстории = Object[Destination].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗаписьИстории, ЗаписьЖурналаРегистрации);
		ЗаполнитьЗначенияСвойств(НоваяЗаписьИстории, ОписаниеСобытия);
 		
		RecordsLoaded = RecordsLoaded + 1;

	КонецЦикла;
	
	Object.Записать();

КонецПроцедуры

#EndRegion

#Region Private

// Возвращает структурированное описание события по данным записи журнала регистрации.
// Описание строится по данным колонки "Event" таблицы значений, полученной выгрузкой через
// ГлобальныйКонтекст.ВыгрузитьЖурналРегистрации.
// Формат строки события: "ИмяОбъекта.Операция.Действие1.Действие2...ДействиеN[.КодОшибки]".  
// 
// Параметры:
// 	ЗаписьЖурналаРегистрации - СтрокаТаблицыЗначений - строка таблицы значений выгрузки из журнала регистрации;
//
// Returns:
// 	Структура - описание:
// * Event - Строка - полный текст события записи журнала регистрации;
// * ObjectName - Строка - абстракция, к которой привязывается событие, для отделения его от глобального контекста;
// * Action - Строка - операция, к которой привязывается событие;
// * EventPresentation - Строка - представление события в формате: "Действие1.Действие2...ДействиеN";
// * HTTPStatusCode - Число - код состояния HTTP или 0, если кода нет; 
//
Функция ОписаниеСобытияПоЗаписиЖурналаРегистрации( Знач ЗаписьЖурналаРегистрации )
	
	Var ЭлементыСобытия;
	Var Результат;

	MIN_WORD_COUNT = 3;
	
	Результат = Новый Структура();
	Результат.Вставить( "Event", ЗаписьЖурналаРегистрации.Event );
	Результат.Вставить( "ObjectName", "" );
	Результат.Вставить( "Action", "" );
	Результат.Вставить( "EventPresentation", "" );
	Результат.Вставить( "HTTPStatusCode", 0 );
	ЭлементыСобытия = СтрРазделить( ЗаписьЖурналаРегистрации.Event, "." );
	
	Если ЭлементыСобытия.Количество() < MIN_WORD_COUNT Тогда
		
		Возврат Результат;
							
	КонецЕсли;

	Результат.ObjectName = ЭлементыСобытия[ 0 ];
	Результат.Action = ЭлементыСобытия[ 1 ];
	Результат.HTTPStatusCode = КодСостоянияHTTP( ЭлементыСобытия );
	Результат.EventPresentation = ПредставлениеСобытия( ЭлементыСобытия, Результат.HTTPStatusCode );
	
	Возврат Результат;
	
КонецФункции

Функция КодСостоянияHTTP( Знач ЭлементыСобытия )

	Var ОписаниеТипа;
	Var ПоследнийЭлемент;
	
	ПоследнийЭлемент = ЭлементыСобытия[ ЭлементыСобытия.ВГраница() ];
	ОписаниеТипа = Новый ОписаниеТипов( "Число" );
	
	Возврат ОписаниеТипа.ПривестиЗначение( ПоследнийЭлемент );
	
КонецФункции

Функция ПредставлениеСобытия( Знач ЭлементыСобытия, Знач КодСостоянияHTTP )

	Var ИндексПоследнегоЭлемента;
	Var Индекс;
	
	ИндексПоследнегоЭлемента = ЭлементыСобытия.ВГраница();
	ГраницаПредставленияСобытия = ?( КодСостоянияHTTP = 0, ИндексПоследнегоЭлемента, ИндексПоследнегоЭлемента - 1 );

	ПредставлениеСобытия = Новый Массив;
	
	Для Индекс = 2 По ГраницаПредставленияСобытия Цикл
		
		ПредставлениеСобытия.Добавить( ЭлементыСобытия[Индекс] );
		
	КонецЦикла;
	
	Возврат СтрСоединить( ПредставлениеСобытия, "." );
	
КонецФункции

#EndRegion