#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере( Отказ, СтандартнаяОбработка )
	
	Если ( Параметры.КлючЗаписи.Пустой() ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьВидФормы( Параметры.ИмяКоманды );
	ЗаполнитьДанныеФормы( Параметры.КлючЗаписи );

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПользовательскийВариантПриИзменении( Элемент )
	
	Перем ТекущиеДанные;
	Перем УдаляемыеДанные;
	Перем ТекстВопроса;
	Перем Оповещение;
	
	ТекущиеДанные = Элементы.Commits.ТекущиеДанные;
	
	Если ( ТекущиеДанные = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ( ТекущиеДанные.IsUserSettings ) Тогда
		
		УстановитьРежимРедактированияJSON( ТекущиеДанные.IsUserSettings );
		
	Иначе
		
		УдаляемыеДанные = Новый Структура( "КлючЗаписи, ТекущиеДанные", Параметры.КлючЗаписи, ТекущиеДанные );
		Оповещение = Новый ОписаниеОповещения( "ПослеЗакрытияВопросаСбросНастроек", ЭтотОбъект, УдаляемыеДанные );
		ТекстВопроса = НСтр( "ru = 'Сбросить настройки маршрутизации на значения по умолчанию?'" );
		
		ПоказатьВопрос( Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет );
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКоммиты

&НаКлиенте
Процедура CommitsПриАктивизацииСтроки(Элемент)
	
	Если ( Элемент.ТекущиеДанные = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьРежимРедактированияJSON( Элемент.ТекущиеДанные.IsUserSettings );
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьСохранениеJSON( Команда )
	
	Перем ТекущиеДанные;
	Перем СохраняемыеДанные;
	Перем Оповещение;
	Перем ТекстВопроса;
	
	ТекущиеДанные = Элементы.Commits.ТекущиеДанные;
	
	Если ( ТекущиеДанные = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;

	СохраняемыеДанные = Новый Структура( "КлючЗаписи, ТекущиеДанные", Параметры.КлючЗаписи, ТекущиеДанные );
	
	Если ( НЕ ПользовательскиеНастройкиСуществуют( Параметры.КлючЗаписи, ТекущиеДанные.CommitSHA ) ) Тогда
		
		СохранитьJSON( СохраняемыеДанные );
		
	Иначе
		
		Оповещение = Новый ОписаниеОповещения( "ПослеЗакрытияВопросаСохранитьJSON", ЭтотОбъект, СохраняемыеДанные );
		ТекстВопроса = НСтр( "ru = 'Пользовательские настройки уже существуют, перезаписать?'" );
		ПоказатьВопрос( Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет );
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидФормы( Знач ИмяКоманды )
	
	Если ( ИмяКоманды = "ОткрытьТекстЗапросаВРедактореJSON" ) Тогда

		ЭтотОбъект.ЭтоЗапрос = Истина;
		ЭтаФорма.Заголовок = "Содержимое запроса";

	ИначеЕсли ( ИмяКоманды = "ОткрытьНастройкиМаршрутизацииВРедактореJSON" ) Тогда
		
		ЭтотОбъект.ЭтоЗапрос = Ложь;
		ЭтаФорма.Заголовок = "Настройка маршрутизации";
		
	Иначе
		
		ВызватьИсключение НСтр("ru = 'Недопустимая команда открытия редактора JSON.'");
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормы( Знач КлючЗаписи )
	
	Перем Query;
	Перем QueryCommits;
	Перем Settings;
	Перем UserSettings;
	Перем НоваяСтрока;
	
	Query = ОбработчикиСобытий.ЗагрузитьДанныеЗапроса( КлючЗаписи.ОбработчикСобытия, КлючЗаписи.Ключ );
	
	Если ( Query = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ( ЭтотОбъект.ЭтоЗапрос ) Тогда
		
		НоваяСтрока = ЭтотОбъект.Commits.Добавить();
		НоваяСтрока.JSON = Query.Получить( "json" );
		
	Иначе

		QueryCommits = Query.Получить( "commits" );
		
		Для каждого Commit Из QueryCommits Цикл
			
			НоваяСтрока = ЭтотОбъект.Commits.Добавить();
			НоваяСтрока.CommitSHA = Commit.Получить( "id" );
			
			UserSettings = Commit.Получить( "user_settings" );
			
			Settings = ?( UserSettings = Неопределено, Commit.Получить("settings"),  UserSettings );
			
			Если ( Settings = Неопределено ) Тогда

				Продолжить;
				
			КонецЕсли;
			
			НоваяСтрока.JSON = Settings.Получить( "json" );
			НоваяСтрока.IsUserSettings = ( UserSettings <> Неопределено );
			
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЭлементовФормы()
	
	Если ( ЭтотОбъект.ЭтоЗапрос ) Тогда
		
		Элементы.ГруппаCommits.Видимость = Ложь;
		Элементы.ГруппаПользовательскийВариант.Видимость = Ложь;
		Элементы.CommitsJSONQuery.Видимость = Истина;
		
	Иначе
		
		Элементы.CommitsJSONRouting.Видимость = Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьРежимРедактированияJSON( Знач Редактировать = Ложь )
	
	Перем ЭлементРедактированияJSON;
	
	Если ( ЭтотОбъект.ЭтоЗапрос ) Тогда
		
		ЭлементРедактированияJSON  = Элементы.CommitsJSONQuery;
		
	Иначе

		ЭлементРедактированияJSON = Элементы.CommitsJSONRouting;
		
	КонецЕсли;
	
	ЭлементРедактированияJSON.ТолькоПросмотр = НЕ Редактировать;
	Элементы.СохранитьJSON.Доступность = Редактировать;
		
КонецПроцедуры

&НаСервереБезКонтекста 
Функция FindCommitById( Знач ДанныеЗапроса, Знач CommitSHA )
	
	Перем QueryCommits;

	QueryCommits = ДанныеЗапроса.Получить("commits");
	
	Для каждого Commit Из QueryCommits Цикл

		Если ( Commit.Получить("id") <> CommitSHA ) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Возврат Commit;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УдалитьПользовательскиеНастройки( Знач КлючЗаписи, Знач CommitSHA, ТекущиеНастройки )
	
	Перем ДанныеЗапроса;
	Перем Commit;
	Перем ПервоначальныеНастройки;

	ДанныеЗапроса = ОбработчикиСобытий.ЗагрузитьДанныеЗапроса( КлючЗаписи.ОбработчикСобытия, КлючЗаписи.Ключ );
	
	Если ( НЕ ЗначениеЗаполнено(ДанныеЗапроса) ) Тогда
		
		Возврат;
		
	КонецЕсли;

	Commit = FindCommitById( ДанныеЗапроса, CommitSHA );
	ПервоначальныеНастройки = Commit.Получить( "settings" ).Получить( "json" );
	Commit.Удалить( "user_settings" );
	
	Если ( ПустаяСтрока(ПервоначальныеНастройки) ИЛИ ПервоначальныеНастройки = ТекущиеНастройки ) Тогда
		
		Возврат;
		
	КонецЕсли;		
				
	ОбработчикиСобытий.СохранитьДанныеЗапроса( КлючЗаписи.ОбработчикСобытия, КлючЗаписи.Ключ, ДанныеЗапроса );
	
	ТекущиеНастройки = ПервоначальныеНастройки; 

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаСбросНастроек( РезультатВопроса, ДополнительныеПараметры ) Экспорт
	
	Перем ТекущиеДанные;
	
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	
	Если ( РезультатВопроса = КодВозвратаДиалога.Нет ) Тогда
		
		ТекущиеДанные.IsUserSettings = Истина;
		
        Возврат;
        
	КонецЕсли;

	УдалитьПользовательскиеНастройки( ДополнительныеПараметры.КлючЗаписи, ТекущиеДанные.CommitSHA, ТекущиеДанные.JSON );
	
	УстановитьРежимРедактированияJSON( Ложь );

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПользовательскиеНастройкиСуществуют( Знач КлючЗаписи, Знач CommitSHA )

	Перем ДанныеЗапроса;
	Перем Commit;
	
	ДанныеЗапроса = ОбработчикиСобытий.ЗагрузитьДанныеЗапроса( КлючЗаписи.ОбработчикСобытия, КлючЗаписи.Ключ );
	Commit = FindCommitById( ДанныеЗапроса, CommitSHA );
	
	Возврат ( Commit.Получить("user_settings") <> Неопределено );
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДополнитьДанныеЗапросаПользовательскимиНастройками( ДанныеЗапроса, Знач CommitSHA, Знач JSON ) Экспорт
	
	Перем Commit;
	Перем ПользовательскиеНастройки;
	Перем Поток;
	
	Commit = FindCommitById( ДанныеЗапроса, CommitSHA );

	Поток = ПолучитьДвоичныеДанныеИзСтроки( JSON, КодировкаТекста.UTF8 ).ОткрытьПотокДляЧтения();
	ПользовательскиеНастройки = КоннекторHTTP.JsonВОбъект( Поток );
	ОбщегоНазначения.ДополнитьКоллекциюТекстомИзПотока( Поток, "json", ПользовательскиеНастройки );
	
	Commit.Вставить( "user_settings", ПользовательскиеНастройки );		

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьJSONНаСервере( Знач КлючЗаписи, Знач CommitSHA, Знач JSON )
	
	Перем ДанныеЗапроса;
	
	ДанныеЗапроса = ОбработчикиСобытий.ЗагрузитьДанныеЗапроса( КлючЗаписи.ОбработчикСобытия, КлючЗаписи.Ключ );
	
	Если ( ДанныеЗапроса = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДополнитьДанныеЗапросаПользовательскимиНастройками( ДанныеЗапроса, CommitSHA, JSON );
	
	ОбработчикиСобытий.СохранитьДанныеЗапроса(КлючЗаписи.ОбработчикСобытия, КлючЗаписи.Ключ, ДанныеЗапроса );	

КонецПроцедуры

&НаКлиенте
Процедура СохранитьJSON( Знач СохраняемыеДанные )
	
	Перем ТекущиеДанные;
	Перем ИнформацияОбОшибке;
	Перем ТекстПричиныОшибки;
	Перем ДополнительнаяИнформация; 
	
	Попытка
		
		ТекущиеДанные = СохраняемыеДанные.ТекущиеДанные;
		СохранитьJSONНаСервере( СохраняемыеДанные.КлючЗаписи, ТекущиеДанные.CommitSHA, ТекущиеДанные.JSON );
		
	Исключение
		
		ТекстПричиныОшибки = НСтр("ru = 'Непредвиденный символ при чтении JSON'");
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		Если ( ИнформацияОбОшибке.ЯвляетсяОшибкойКатегории(КатегорияОшибки.ОшибкаВоВремяВыполненияВстроенногоЯзыка)
			И ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке) = ТекстПричиныОшибки ) Тогда

			ДополнительнаяИнформация = НСтр("ru = 'Текст должен быть в формате JSON.'");
			ОбработкаОшибок.ПоказатьИнформациюОбОшибке( ИнформацияОбОшибке,
														ВариантОтображенияСообщенияОбОшибке.КраткоеПредставлениеОшибки,
														ДополнительнаяИнформация );
	
		Иначе
			
			ОбработкаОшибок.ПоказатьИнформациюОбОшибке( ИнформацияОбОшибке );
			
		КонецЕсли;
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаСохранитьJSON( РезультатВопроса, ДополнительныеПараметры ) Экспорт
	
	Если ( РезультатВопроса = КодВозвратаДиалога.Нет ) Тогда
		
        Возврат;
        
    КонецЕсли;

	СохранитьJSON( ДополнительныеПараметры );

КонецПроцедуры

#КонецОбласти