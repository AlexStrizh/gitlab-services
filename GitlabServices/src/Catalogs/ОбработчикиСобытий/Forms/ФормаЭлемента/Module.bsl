#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии( Отказ )
	
	УстановитьОтборыСписков( Объект.Ссылка );
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИмяТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьИсториюСобытий(Команда)
	
	ОткрытьФорму("Справочник.ОбработчикиСобытий.Форма.УсловияОтбора",
				 ,
				 ЭтаФорма,
				 Новый УникальныйИдентификатор(),
				 ,
				 ,
				 Новый ОписаниеОповещения("ЗагрузитьИсториюСобытийЗавершение", ЭтаФорма),
				 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекстЗапросаВРедактореJSON( Команда )
	
	ОткрытьРедакторJSON( ЭтотОбъект.Элементы.ПолученныеЗапросы.ТекущаяСтрока, Команда );

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиМаршрутизацииВРедактореJSON( Команда )
	
	ОткрытьРедакторJSON( ЭтотОбъект.Элементы.ПолученныеЗапросы.ТекущаяСтрока, Команда );

КонецПроцедуры

&НаКлиенте
Процедура РучнаяОтправкаДанных(Команда)
	
	Перем ТекущиеДанные;
	Перем ТекстВопроса;
	
	ТекущиеДанные = ЭтотОбъект.Элементы.ПолученныеЗапросы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтрШаблон(НСтр("ru = 'Продолжить выполнение операции для %1?'"), ТекущиеДанные.Ключ);
	ПоказатьВопрос(Новый ОписаниеОповещения("РучнаяОтправкаДанныхЗавершение", ЭтотОбъект),
		ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0);
		
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьФоновыеЗадания(Команда)
	
	Перем ТекущаяСтрока;
	Перем ПараметрыОткрытия;
	
	ТекущаяСтрока = ЭтотОбъект.Элементы.ПолученныеЗапросы.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("КлючЗаписи", ТекущаяСтрока);

	ОткрытьФорму("Справочник.ОбработчикиСобытий.Форма.ФоновыеЗадания",
				 ПараметрыОткрытия,
				 ЭтаФорма,
				 ,
				 ,
				 ,
				 ,
				 РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				 
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСайтСMergeRequests(Команда)
	
	Перем ТекущаяСтрока;
	Перем ОписаниеОповещения;
	Перем URL;
	
	ТекущаяСтрока = ЭтотОбъект.Элементы.ПолученныеЗапросы.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	URL = MergeRequestsURL(ТекущаяСтрока);
	
	Если НЕ ПустаяСтрока(URL) Тогда
		#Если ВебКлиент Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияРасширенияРаботыСФайлами", Этаформа, URL);
			НачатьПодключениеРасширенияРаботыСФайлами(ОписаниеОповещения);
		#Иначе
			ЗапуститьПриложение(URL);
   		#КонецЕсли		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтбор( Знач Список, Знач Ключ, Знач Значение )
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки( Список,
															Ключ,
															ВидСравненияКомпоновкиДанных.Равно,
															Значение );

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборыСписков( Знач Ссылка )
	
	УстановитьОтбор( ПолученныеЗапросы.Отбор, "ОбработчикСобытия", Объект.Ссылка );
	УстановитьОтбор( ВнешниеФайлы.Отбор, "ОбработчикСобытия", Объект.Ссылка );
	УстановитьОтбор( ИсторияСобытий.Отбор, "Ссылка", Объект.Ссылка );
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРедакторJSON( Знач ТекущаяСтрока, Знач Команда )
	
	Если ( ТекущаяСтрока = Неопределено ) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить( "RecordKey", ТекущаяСтрока );
	ПараметрыОткрытия.Вставить( "CommandName", Команда.Имя );

	ОткрытьФорму( "Справочник.ОбработчикиСобытий.Форма.EditorJSON",
				ПараметрыОткрытия,
				ЭтаФорма,
				,
				,
				,
				,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца );
	
КонецПроцедуры









&НаКлиенте
Процедура ЗагрузитьИсториюСобытийЗавершение(РезультатЗакрытия, Параметры) Экспорт
	
	Перем ДобавленоЗаписей;
	Перем Таймаут;
	
	Таймаут = 5;
	
	ДобавленоЗаписей = 0;	
	ЗагрузитьИсториюСобытийЗавершениеНаСервере(РезультатЗакрытия, Параметры, ДобавленоЗаписей);
	ПоказатьПредупреждение( , НСтр("ru = 'Добавлено записей: '") + Строка(ДобавленоЗаписей),
			Таймаут, НСтр("ru = 'Загрузка истории из журнала регистрации'"));
							   	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИсториюСобытийЗавершениеНаСервере(РезультатЗакрытия, Параметры, ДобавленоЗаписей)
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ДатаНачала", РезультатЗакрытия.ДатаНачала);
	ПараметрыОтбора.Вставить("ДатаОкончания", РезультатЗакрытия.ДатаОкончания);
	ПараметрыОтбора.Вставить("Метаданные", Метаданные.Справочники.ОбработчикиСобытий);
	ПараметрыОтбора.Вставить("Данные", Объект.Ссылка);
	
	ДобавленоЗаписей = 0;
	Логирование.ДекораторЗагрузитьИсториюСобытий(Метаданные.Справочники.ОбработчикиСобытий,
														"ИсторияСобытий",
														ПараметрыОтбора,
														ДобавленоЗаписей);
	
	ЭтаФорма.Элементы.ИсторияСобытий.Обновить();
		
КонецПроцедуры

&НаКлиенте
Процедура РучнаяОтправкаДанныхЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Перем ТекущаяСтрока;
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = ЭтотОбъект.Элементы.ПолученныеЗапросы.ТекущаяСтрока;
	РучнаяОтправкаДанныхНаСервере(Объект.Ссылка, ТекущаяСтрока);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура РучнаяОтправкаДанныхНаСервере(Знач Ссылка, Знач КлючЗапроса)
	Справочники.ОбработчикиСобытий.ЗапуститьОбработкуДанныхВручную(Ссылка, КлючЗапроса);
КонецПроцедуры

&НаСервереБезКонтекста
Функция MergeRequestsURL(Знач КлючЗапроса)
	
	Перем ДанныеТелаЗапроса;
	Перем Запрос;
	Перем ПараметрыЗапроса;
	Перем MergeRequests;
	Перем Ключ;
	Перем URL;
	Перем Результат;
	
	//ДанныеТелаЗапроса = РегистрыСведений.ДанныеОбработчиковСобытий.ПолучитьДанныеТелаЗапроса(КлючЗапроса);
	ДанныеТелаЗапроса = ОбработчикиСобытий.ЗагрузитьДанныеЗапроса( КлючЗапроса.ОбработчикСобытия, КлючЗапроса.Ключ );
	
	Результат = "";
	
	Если ТипЗнч(ДанныеТелаЗапроса) <> Тип("Соответствие") Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = СервисыGitLab.СформироватьЗапросНаMergeRequests(ДанныеТелаЗапроса);
	
	ПараметрыЗапроса = Новый Соответствие;
	ПараметрыЗапроса.Вставить("PRIVATE-TOKEN", Запрос.СекретныйКлюч);
	
	MergeRequests = КоннекторHTTP.GetJson(Запрос.URL, ПараметрыЗапроса);
	
	// Значит запрос вернул не JSON, а ошибку или что-то еще, что нам не нужно.
	Если ТипЗнч(MergeRequests) = Тип("Соответствие") Тогда
		Возврат Результат;
	КонецЕсли;

	Для каждого MergeRequest Из MergeRequests Цикл
		
		Ключ = MergeRequest.Получить("merge_commit_sha");
		Если Ключ = Неопределено ИЛИ Ключ <> КлючЗапроса.Ключ Тогда
			Продолжить;
		КонецЕсли;
		
		URL = MergeRequest.Получить("web_url");
		Если URL <> Неопределено Тогда
			Результат = URL;
			Прервать;
		КонецЕсли;

	КонецЦикла;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ПослеПодключенияРасширенияРаботыСФайлами(Подключено, ДополнительныеПараметры)Экспорт
	
	Перем ОписаниеОповещения;
	 
    Если Подключено Тогда
        НачатьЗапускПриложения(Новый ОписаниеОповещения("ПослеЗапуска", Этаформа), ДополнительныеПараметры);
    Иначе
        ОписаниеОповещения = Новый ОписаниеОповещения("ПослеУстановкиРасширенияРаботыСФайлами",
        												Этаформа,
        												ДополнительныеПараметры);
        НачатьУстановкуРасширенияРаботыСФайлами(ОписаниеОповещения);
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеУстановкиРасширенияРаботыСФайлами(ДополнительныеПараметры) Экспорт
	
	Перем ОписаниеОповещения;
	 
    ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияРасширенияРаботыСФайлами",
    													Этаформа,
    													ДополнительныеПараметры);
    НачатьПодключениеРасширенияРаботыСФайлами(ОписаниеОповещения)
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапуска(КодВозврата, ДополнительныеПараметры) Экспорт 

КонецПроцедуры

#КонецОбласти



