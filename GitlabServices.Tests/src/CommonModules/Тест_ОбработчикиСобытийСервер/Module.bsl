#Область СлужебныйПрограммныйИнтерфейс

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура НайтиПоСекретномуКлючуПустаяСсылкаЕслиПараметрЧисло(Фреймворк) Экспорт

	// given
	УдалитьВсеОбработчикиСобытий();
	// when
	Результат = ОбработчикиСобытий.НайтиПоСекретномуКлючу(1);
	// then
	Фреймворк.ПроверитьНеЗаполненность(Результат);

КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура НайтиПоСекретномуКлючуПустаяСсылкаЕслиПараметрПустаяСтрока(Фреймворк) Экспорт

	// given
	УдалитьВсеОбработчикиСобытий();
	// when
	Результат = ОбработчикиСобытий.НайтиПоСекретномуКлючу("");
	// then
	Фреймворк.ПроверитьНеЗаполненность(Результат);

КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура НайтиПоСекретномуКлючуПустаяСсылкаЕслиПараметрНеопределено(Фреймворк) Экспорт

	// given
	УдалитьВсеОбработчикиСобытий();
	// when
	Результат = ОбработчикиСобытий.НайтиПоСекретномуКлючу(Неопределено);
	// then
	Фреймворк.ПроверитьНеЗаполненность(Результат);

КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура НайтиПоСекретномуКлючуПустаяСсылкаЕслиНеНайдено(Фреймворк) Экспорт

	// given
	УдалитьВсеОбработчикиСобытий();
	// when
	Результат = ОбработчикиСобытий.НайтиПоСекретномуКлючу("блаблаблаблака");
	// then
	Фреймворк.ПроверитьНеЗаполненность(Результат);

КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура НайтиПоСекретномуКлючу(Фреймворк) Экспорт
	
	// given
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест2", СекретныйКлюч);
	Код = ОбработчикСобытия.Ссылка.Код;
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест3", СекретныйКлюч + "Бадабумс");
	// when	
	Результат = ОбработчикиСобытий.НайтиПоСекретномуКлючу(СекретныйКлюч);
	// then
	Фреймворк.Что(Результат.Код).Равно(Код);
	
КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура СохранитьДанныеЗапросаУспешно(Фреймворк) Экспорт
	
	// given
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	Данные = "Тест";
	НаборЗаписей = РегистрыСведений.ДанныеЗапросов.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	// when	
	ОбработчикиСобытий.СохранитьДанныеЗапроса(ОбработчикСобытия.Ссылка, "Ключ", Данные);
	// then
	НаборЗаписей = РегистрыСведений[ "ДанныеЗапросов" ].СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ОбработчикСобытия.Установить( ОбработчикСобытия.Ссылка );
	НаборЗаписей.Отбор.Ключ.Установить( "Ключ" );
	НаборЗаписей.Прочитать();
	Фреймворк.ПроверитьРавенство(НаборЗаписей.Количество(), 1);
	Фреймворк.ПроверитьРавенство(НаборЗаписей[0].Данные.Получить(), "Тест");
	
КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура СохранитьДанныеЗапросаЗаписьПредупрежденияВЖурнал(Фреймворк) Экспорт
	
	// given
	ПараметрыОтбораЖурналаРегистрации = Новый Структура("ДатаНачала, Уровень, Событие", ТекущаяДата(), УровеньЖурналаРегистрации.Предупреждение, "ОбработчикиСобытий.Core.СохранениеДанных");
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	Данные = "Тест";
	НаборЗаписей = РегистрыСведений.ДанныеЗапросов.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	// when
	// Искуственная ошибка, проверяем лишь факт записи предупреждения в журнал.
	ОбработчикиСобытий.СохранитьДанныеЗапроса(Справочники.ОбработчикиСобытий.ПустаяСсылка(), "Ключ", Данные);
	Тест_ОбщийМодульСервер.Пауза(2);
	Результат = Новый ТаблицаЗначений();
	ВыгрузитьЖурналРегистрации(Результат, ПараметрыОтбораЖурналаРегистрации, "Дата, ПредставлениеСобытия, ПредставлениеМетаданных, ПредставлениеДанных, Комментарий");
	// then
	Фреймворк.ПроверитьВхождение(Результат[0].Комментарий, "[ ДанныеЗапросов ]: Данные не были сохранены.");
	
КонецПроцедуры

// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура СохранитьВнешниеФайлыУспешно(Фреймворк) Экспорт
	
	// given
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	Данные = "Тест";
	НаборЗаписей = РегистрыСведений.ВнешниеФайлы.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	// when	
	ОбработчикиСобытий.СохранитьВнешниеФайлы(ОбработчикСобытия.Ссылка, "Ключ", Данные);
	// then
	НаборЗаписей = РегистрыСведений[ "ВнешниеФайлы" ].СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ОбработчикСобытия.Установить( ОбработчикСобытия.Ссылка );
	НаборЗаписей.Отбор.Ключ.Установить( "Ключ" );
	НаборЗаписей.Прочитать();
	Фреймворк.ПроверитьРавенство(НаборЗаписей.Количество(), 1);
	Фреймворк.ПроверитьРавенство(НаборЗаписей[0].Данные.Получить(), "Тест");
	
КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура СохранитьВнешниеФайлыЗаписьПредупрежденияВЖурнал(Фреймворк) Экспорт
	
	// given
	ПараметрыОтбораЖурналаРегистрации = Новый Структура("ДатаНачала, Уровень, Событие",	ТекущаяДата(), УровеньЖурналаРегистрации.Предупреждение, "ОбработчикиСобытий.Core.СохранениеДанных");
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	Данные = "Тест";
	НаборЗаписей = РегистрыСведений.ВнешниеФайлы.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	// when
	// Искуственная ошибка, проверяем лишь факт записи предупреждения в журнал.
	ОбработчикиСобытий.СохранитьВнешниеФайлы(Справочники.ОбработчикиСобытий.ПустаяСсылка(), "Ключ", Данные);
	Тест_ОбщийМодульСервер.Пауза(2);
	Результат = Новый ТаблицаЗначений();
	ВыгрузитьЖурналРегистрации(Результат, ПараметрыОтбораЖурналаРегистрации, "Дата, ПредставлениеСобытия, ПредставлениеМетаданных, ПредставлениеДанных, Комментарий");
	// then
	Фреймворк.ПроверитьВхождение(Результат[0].Комментарий, "[ ВнешниеФайлы ]: Данные не были сохранены.");
	
КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура ЗагрузитьВнешниеФайлы(Фреймворк) Экспорт
	
	// given
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	
	Данные = Новый ТаблицаЗначений;
	Данные.Колонки.Добавить("Колонка");
	НоваяСтрока = Данные.Добавить();
	НоваяСтрока.Колонка = "Тест";
	
	МенеджерЗаписи = РегистрыСведений.ВнешниеФайлы.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ОбработчикСобытия = ОбработчикСобытия.Ссылка;
	МенеджерЗаписи.Ключ = "Ключ";
	МенеджерЗаписи.Данные = Новый ХранилищеЗначения(Данные);
	МенеджерЗаписи.Записать();
	// when	
	Результат = ОбработчикиСобытий.ЗагрузитьВнешниеФайлы( ОбработчикСобытия.Ссылка, "Ключ" );
	// then
	Фреймворк.ПроверитьТип(Результат, "ТаблицаЗначений");
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 1);
	
КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура ЗагрузитьВнешниеФайлыОтсутствуютДанные(Фреймворк) Экспорт
	
	// given
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	НаборЗаписей = РегистрыСведений.ВнешниеФайлы.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	// when	
	Результат = ОбработчикиСобытий.ЗагрузитьВнешниеФайлы( ОбработчикСобытия.Ссылка, "Ключ" );
	// then
	Фреймворк.ПроверитьТип(Результат, "ТаблицаЗначений");
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 0);
	
КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура ЗагрузитьДанныеЗапроса(Фреймворк) Экспорт
	
	// given
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	
	Данные = Новый Соответствие();
	Данные.Вставить("Ключ", "Значение");
	
	МенеджерЗаписи = РегистрыСведений.ДанныеЗапросов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ОбработчикСобытия = ОбработчикСобытия.Ссылка;
	МенеджерЗаписи.Ключ = "Ключ";
	МенеджерЗаписи.Данные = Новый ХранилищеЗначения(Данные);
	МенеджерЗаписи.Записать();
	// when	
	Результат = ОбработчикиСобытий.ЗагрузитьДанныеЗапроса( ОбработчикСобытия.Ссылка, "Ключ" );
	// then
	Фреймворк.ПроверитьТип(Результат, "Соответствие");
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 1);
	Фреймворк.ПроверитьРавенство(Результат.Получить("Ключ"), "Значение");
	
КонецПроцедуры

// @unit-test
// Параметры:
// 	Фреймворк - ФреймворкТестирования - Фреймворк тестирования
//
Процедура ЗагрузитьДанныеЗапросаОтсутствуютДанные(Фреймворк) Экспорт
	
	// given
	СекретныйКлюч = "ЮнитТест";
	УдалитьВсеОбработчикиСобытий();
	ОбработчикСобытия = ДобавитьОбработчикСобытий("ЮнитТест1", СекретныйКлюч);
	НаборЗаписей = РегистрыСведений.ДанныеЗапросов.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	// when	
	Результат = ОбработчикиСобытий.ЗагрузитьДанныеЗапроса( ОбработчикСобытия.Ссылка, "Ключ" );
	// then
	Фреймворк.ПроверитьТип(Результат, "Соответствие");
	Фреймворк.ПроверитьРавенство(Результат.Количество(), 0);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьОбработчикСобытий(Знач Наименование = "", Знач СекретныйКлюч = "") Экспорт
	
		ОбработчикСобытия = Справочники.ОбработчикиСобытий.СоздатьЭлемент();
		ОбработчикСобытия.Наименование = Наименование;
		ОбработчикСобытия.СекретныйКлюч = СекретныйКлюч;
		ОбработчикСобытия.Записать();
		
		Возврат ОбработчикСобытия;
	
КонецФункции

Процедура УдалитьВсеОбработчикиСобытий() Экспорт
	
	ВсеЭлементы = Справочники.ОбработчикиСобытий.Выбрать();
	Пока ВсеЭлементы.Следующий() Цикл
		Объект = ВсеЭлементы.ПолучитьОбъект();
		Объект.Удалить();
	КонецЦикла;

КонецПроцедуры
	
#КонецОбласти