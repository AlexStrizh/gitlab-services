#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Ru

Функция Сервер ( Знач URL ) Экспорт
	
	Возврат Server( URL );
	
КонецФункции

Функция Когда( Знач Запрос ) Экспорт
	
	Возврат When( Запрос );
	
КонецФункции

Процедура Сбросить() Экспорт
	
	Reset();	

КонецПроцедуры

Процедура Ожидаем( Ответ = Неопределено ) Экспорт
	
	Expectation( Ответ );
	
КонецПроцедуры

Процедура ИмпортироватьНастройки( Знач Источник, Знач Фильтр = "" ) Экспорт
	
	OpenAPIExpectation( Источник, Фильтр );
	
КонецПроцедуры

Функция Результат() Экспорт
	
	Возврат Result();
	
КонецФункции

//Процедура Очистить() Экспорт
//КонецПроцедуры
//
//Процедура openapi() Экспорт
//КонецПроцедуры

Функция Запрос() Экспорт
	
	Если ЭтотОбъект.Конструктор = Неопределено Тогда
		ЭтотОбъект.Конструктор = Новый Соответствие();
	КонецЕсли;
	ЭтотОбъект.Конструктор.Вставить("httpRequest", Новый Соответствие());
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Метод( Знач Метод = "" ) Экспорт
	
	Результат = ЭтотОбъект.Конструктор["httpRequest"];
	Результат.Вставить("method", Метод);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Путь( Знач Путь = "" ) Экспорт
	
	Результат = ЭтотОбъект.Конструктор["httpRequest"];
	Результат.Вставить("path", Путь);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Заголовки( Знач Заголовки = Неопределено ) Экспорт
	
	Результат = ЭтотОбъект.Конструктор["httpRequest"];
	Результат.Вставить("headers", Заголовки);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Заголовок( Знач Ключ, Знач Значение ) Экспорт
	
	Результат = Новый Соответствие();
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		Массив = Новый Массив;
		Массив.Добавить(Значение);
		Результат.Вставить(Ключ, Массив);
	Иначе
		Результат.Вставить(Ключ, Значение);
	КонецЕсли;
	Если ЭтотОбъект.Конструктор.Получить("headers") = Неопределено Тогда
		Заголовки();
	КонецЕсли;
	ЭтотОбъект.Конструктор["httpRequest"]["headers"] = Результат;	
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Ответ() Экспорт

	Если ЭтотОбъект.Конструктор = Неопределено Тогда
		ЭтотОбъект.Конструктор = Новый Соответствие();
	КонецЕсли;
	ЭтотОбъект.Конструктор.Вставить("httpResponse", Новый Соответствие());
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция КодОтвета(Знач КодОтвета ) Экспорт
	
	Результат = ЭтотОбъект.Конструктор["httpResponse"];
	Результат.Вставить("statusCode", КодОтвета);
	
	Возврат ЭтотОбъект;
	
КонецФункции

#КонецОбласти

#Область En
	
Функция Server( Знач URL ) Экспорт
	
	ЭтотОбъект.URL = URL;
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция When( Знач Запрос ) Экспорт
	
	ЭтотОбъект.Запрос = Запрос;
	
	Возврат ЭтотОбъект;
	
КонецФункции

Процедура Reset() Экспорт

	ЭтотОбъект.MockServerResponse = КоннекторHTTP.Put(URL + "/mockserver/reset");
	
КонецПроцедуры

Процедура Expectation( Ответ = Неопределено ) Экспорт
	
	ЭтотОбъект.Ответ = Ответ;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	ДополнительныеПараметры = Новый Структура("Заголовки", Заголовки);
	ЭтотОбъект.MockServerResponse = КоннекторHTTP.Put(URL + "/mockserver/expectation", JSON(), ДополнительныеПараметры);
	
	Если НЕ КодОтветаHTTP.isCreated(ЭтотОбъект.MockServerResponse.КодСостояния) Тогда
		
		ВызватьИсключение "MockServer: can't create Expectation.";
		
	КонецЕсли;
	
КонецПроцедуры

Процедура OpenAPIExpectation( Знач Источник, Знач Фильтр = "" ) Экспорт
	
	JSON = "{
	|    ""specUrlOrPayload"": ""%1""%2
    |}";
    
    Если НЕ ПустаяСтрока(Фильтр) Тогда
    	
    	Фильтр = ",
			|    ""operationsAndResponses"": {"
			+ Фильтр + "
			|    }";
    	
    КонецЕсли;
    
    JSON = СтрШаблон(JSON, Источник, Фильтр);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	ДополнительныеПараметры = Новый Структура("Заголовки", Заголовки);
	ЭтотОбъект.MockServerResponse = КоннекторHTTP.Put(URL + "/mockserver/openapi", JSON, ДополнительныеПараметры);
	
	Если НЕ КодОтветаHTTP.isCreated(ЭтотОбъект.MockServerResponse.КодСостояния) Тогда
		
		ВызватьИсключение "MockServer: can't create openAPIExpectation.";
		
	КонецЕсли;
	
КонецПроцедуры

Функция Result() Экспорт
	
	Возврат ЭтотОбъект.MockServerResponse;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция JSON()
	
	Перем JSON;
	
	JSON = "{";
	
	Если ( ТипЗнч(Запрос) = Тип("Строка") И НЕ ПустаяСтрока(Запрос) ) Тогда
		
		JSON = JSON + "
			|    ""httpRequest"": {";
		JSON = JSON + Запрос;
		Если Ответ = Неопределено Тогда
			JSON = JSON + "
				|    }";
		Иначе
			JSON = JSON + "
				|    },";			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ( ТипЗнч(Ответ) = Тип("Строка") И НЕ ПустаяСтрока(Ответ) ) Тогда
		
		JSON = JSON + "
			|    ""httpResponse"": {";
		JSON = JSON + Ответ;
		JSON = JSON + "
			|    }";

	КонецЕсли;
	
	JSON = JSON + "
			|}";
	
	Возврат JSON;
	
КонецФункции

#КонецОбласти

#КонецЕсли
